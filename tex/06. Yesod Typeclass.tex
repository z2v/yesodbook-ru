\chapter {Класс типов Yesod}\label{chap:yesod-typeclass}

Каждое ваше Yesod-приложение требует наличия экземпляра класса типов \lstinline!Yesod!. До этого мы видели только \lstinline!defaultLayout!. В этой главе мы рассмотрим предназначение многих методов класса типов \lstinline!Yesod!.

% REVIEW Нужно обсудить, перевод неочевиден. Я поправил в соответствии со своим
% представлением о том, что автор имел в виду, но я могу ошибаться.
Класс типов \lstinline!Yesod! является основным местом для определения настроек приложения. Для всех методов этого класса типов имеются определения по умолчанию, которые обычно делают то, что требуется. Но чтобы создать мощное, соответствующее требованиям заказчика приложение, вам, скорее всего, потребуется переопределить по меньшей мере некоторые из этих методов.

\section {Рендеринг и разбор URL}

Мы уже отмечали, что в Yesod есть возможность автоматического рендеринга типобезопасных URL в текстовое представление, которое может быть вставлено в HTML-страницу. Допустим, у нас есть определение маршрута, которое выглядит следующим образом:

\begin{lstlisting}
mkYesod "MyApp" [parseRoutes|
/some/path SomePathR GET
]
\end{lstlisting}

Если мы используем \lstinline!SomePath! в hamlet-шаблоне, то во что Yesod его отобразит? Yesod всегда пытается сформировать \emph{абсолютные} URL. Это особенно полезно, когда мы начинаем создавать XML-карты сайта и Atom-ленты или рассылать электронную почту. Но чтобы сформировать абсолютный URL, нам необходимо знать доменное имя приложения.

Возможно, вы считаете, что можно получить эту информацию из запроса пользователя, но нам также надо учитывать номера портов. И даже если бы мы брали номер порта из запроса, то как узнать, используется ли HTTP или HTTPS? Даже если бы и \emph{это} нам было известно, то это бы означало, что в зависимости от того, как пользователь делает запрос, получались бы различные URL. К примеру, формировались бы различные URL в зависимости от того, обращается ли пользователь к <<example.com>> или к <<www.example.com>>. Для поисковой оптимизации нам бы хотелось иметь возможность объединить эти варианты в одном каноническом URL.

И, наконец, Yesod не делает никаких предположений о том, \emph{где} вы будете разворачивать ваше приложение. К примеру, у нас может быть по большей части статический сайт (http://static.example.com/), но нам также хотелось бы разместить вики, построенную на основе Yesod, по адресу /wiki/. Нет надёжного способа получить из приложения адрес, по которому оно развёрнуто. Таким образом, вместо того, чтобы опираться на догадки, вам надо указать Yesod адрес, который является корнем приложения.

В примере с вики вам нужно определить экземпляр \lstinline!Yesod! следующим образом:

\begin{lstlisting}
instance Yesod MyWiki where
    approot = ApprootStatic "http://static.example.com/wiki"
\end{lstlisting}

Заметьте, что на конце нет косой черты. Далее, когда Yesod будет конструировать URL для \lstinline!SomePathR!, он определит, что относительный путь для \lstinline!SomePathR! выглядит как \lstinline!/some/path!, добавит эту строку к approot и получит в итоге \lstinline!http://static.example.com/wiki/some/path!.

Но, если вы помните, большинство наших примеров не использовало метод \lstinline!approot!. В таких случаях наше приложение получает пустой approot или пустую строку. Это подходит для многих случаев, так как многие приложения работают с корня домена. В таком случае маршрут, аналогичный \lstinline!SomePathR!, будет рендериться как \lstinline!/some/path!, что является корректной относительной ссылкой в любом месте вашего сайта. Однако для некоторых вариантов использования (ленты Atom/RSS или XML-карты сайтов) требуются абсолютные пути. В таком случае вам следует указать approot явно.

Кроме конструктора \lstinline!ApprootStatic!, уже показанного выше, можно использовать конструкторы \lstinline!AppMaster! и \lstinline!AppRequest!. Первый позволяет определить approot из значения основы, что позволяет вам, к примеру, загрузить approot из файла конфигурации. Второй же даёт возможность использовать ещё и данные запроса для определения approot, и с помощью этого вы сможете, например, задавать различные доменные имена в зависимости того, как пользователь обращается к сайту.

Сгенерированный шаблон сайта использует по умолчанию \lstinline!ApprootMaster! и получает approot из конфигурационного файла при запуске. Помимо этого, он загружает различные настройки для сборок разработки, тестирования, подготовки к развёртыванию и боевой сборки. Таким образом, вы с лёгкостью можете проводить тестирование на одном домене, к примеру, на localhost, а запускать приложение на другом домене.

\section {joinPath}

Чтобы конвертировать типобезопасный URL в текстовое значение, Yesod использует две вспомогательные функции. Первая~--- это метод \lstinline!renderRoute! класса типов \lstinline!RenderRoute!. Каждый типобезопасный URL является экземпляром этого класса типов. \lstinline!renderRoute! преобразовывает значение в список компонентов пути. Так \lstinline!SomePathR! из примера выше будет преобразован в \lstinline!["some", "path"]!.

\begin{remark}
На самом деле, \lstinline!renderRoute! создаёт и компоненты пути, и список параметров строки запроса. Определение \lstinline!renderRoute! по умолчанию всегда выдаёт пустой список параметров строки запроса. Но это поведение можно переопределить. Одним из вариантов использования этого является статический подсайт, который добавляет хэш содержимого файла в строку параметров для управления кэшированием.
\end{remark}

Другой функцией является метод \lstinline!joinPath! класса типов Yesod. Эта функция принимает четыре аргумента: значение основы, корень приложения, список компонентов пути и список параметров строки запроса, а возвращает URL в виде текста. Реализация по умолчанию <<делает всё правильно>>: отделяет участки пути при помощи косой черты, добавляет корень приложения в начало и дописывает в конец строку запроса.

Если вас устраивает рендеринг URL в текст по умолчанию, то вам не надо изменять эту функцию. Но если же вам требуется модифицировать рендеринг так, чтобы, например, добавлять косую черту в конце URL, то этот метод будет подходящим местом для того, чтобы сделать это.

\section {cleanPath}

Противоположной функцией по отношению к \lstinline!joinPath! является \lstinline!cleanPath!. Давайте посмотрим, как она используется в процессе диспетчеризации:

\begin{enumerate}
  \item Путь, запрашиваемый пользователем, разбивается на последовательность компонентов пути.
  \item Мы передаём компоненты пути в функцию \lstinline!cleanPath!.
  \item Если \lstinline!cleanPath! предписывает выполнить перенаправление (возвращая \lstinline!Left!), то пользователю возвращается ответ с кодом 301. Такое поведение применяется для принудительного использования канонических URL (например, убирая лишние косые черты).
  \item В противном случае мы пытаемся выполнить диспетчеризацию, используя результат \lstinline!cleanPath! (в данном случае \lstinline!Right!). Если получается, то возвращаем получившийся ответ, иначе возвращаем статус 404.
\end{enumerate}

Такая комбинация даёт подсайтам полный контроль над тем, как будут выглядеть их URL, в то же время позволяя основному сайту иметь изменённые URL. В качестве простого примера давайте посмотрим, как мы можем указать Yesod, что нужно всегда добавлять косую черту в конце URL:

\begin{lstlisting}
{-# LANGUAGE TypeFamilies, QuasiQuotes, MultiParamTypeClasses, TemplateHaskell, OverloadedStrings #-}

import Yesod
import Network.HTTP.Types (encodePath)
import Blaze.ByteString.Builder.Char.Utf8 (fromText)
import qualified Data.Text as T
import qualified Data.Text.Encoding as TE
import Control.Arrow ((***))
import Data.Monoid (mappend)

data Slash = Slash

mkYesod "Slash" [parseRoutes|
/ RootR GET
/foo FooR GET
|]

instance Yesod Slash where
    joinPath _ ar pieces' qs' =
        fromText ar `mappend` encodePath pieces qs
      where
        qs = map (TE.encodeUtf8 *** go) qs'
        go "" = Nothing
        go x = Just $ TE.encodeUtf8 x
        pieces = pieces' ++ [""]

    -- Мы хотим сохранять канонические URL. Поэтому, если на конце URL отсутствует
    -- косая черта, делаем перенаправление. Но пустой набор компонентов пути остаётся
    -- как есть.
    cleanPath _ [] = Right []
    cleanPath _ s
        | dropWhile (not . T.null) s == [""] = -- единственно возможная пустая строка - последняя
            Right $ init s
        -- Т. к. joinPath добавит недостающую косую черту, мы просто
        -- удаляем пустые компоненты пути.
        | otherwise = Left $ filter (not . T.null) s

getRootR = defaultLayout [whamlet|
<p>
    <a href=@{RootR}>RootR
<p>
    <a href=@{FooR}>FooR
|]

getFooR = getRootR

main = warpDebug 3000 Slash
\end{lstlisting}%$

Во-первых, давайте посмотрим на нашу реализацию \lstinline!joinPath!. Она почти полностью скопирована из реализации по умолчанию в Yesod с одним изменением: мы добавляем дополнительную пустую строку в конец. При обработке компонентов пути пустая строка приведёт к добавлению дополнительной косой черты. Таким образом дополнительная пустая строка приведёт к обязательному наличию косой черты на конце.

\lstinline!cleanPath! немного сложнее. Сначала мы, как и раньше, проверяем путь на пустоту, и если он пуст, то оставляем как есть. Мы используем Right, чтобы указать, что нет необходимости в перенаправлении. Следующий клоз в действительности проверяет 2 возможных случая с URL:

\begin{itemize}
  \item В пути имеется две косых черты подряд, которые будут выглядеть как пустая строка в середине списка компонентов пути.
  \item В конце пути нет косой черты, в результате чего последний компонент пути не будет равен пустой строке.
\end{itemize}

Предполагая, что ни одно из этих условий не выполняется, получаем, что только последний компонент является пустой строкой, и нам нужно выполнить диспетчеризацию по списку всех компонентов, за исключением последнего. С другой стороы, если это не так, то нам необходимо выполнить перенаправление на канонический URL. В этом случае мы удаляем все пустые компоненты и не утруждаем себя добавлением косой черты в конец, т. к. \lstinline!joinPath! сделает это за нас.

\section {defaultLayout}

Большинство веб-сайтов используют некий общий шаблон для всех своих страниц. Для реализации этого \lstinline!defaultLayout! является рекомендуемым подходом. Хотя вы и можете так же легко определить свою собственную функцию и вызывать её вместо этого метода, но если вы переопределяете \lstinline!defaultLayout!, то все страницы, генерируемые Yesod (страницы сообщений об ошибках, страницы аутентификации) автоматически получат заданный стиль.

Переопределение делается довольно просто: мы используем \lstinline!widgetToPageContent!, чтобы преобразовать \lstinline!Widget! в заглавие, теги заголовка и тела страницы, а затем используем \lstinline!hamletToRepHtml! для преобразования шаблона Hamlet в \lstinline!RepHtml!. В \lstinline!defaultLayout! мы можем даже добавить дополнительные компоненты виджетов, такие как шаблоны \lstinline!Lucius!. Пример должен сделать это понятным:

\begin{lstlisting}
    defaultLayout contents = do
        PageContent title headTags bodyTags <- widgetToPageContent $ do
            toWidget [cassius|
#body
    font-family: sans-serif
#wrapper
    width: 760px
    margin: 0 auto
|]
            addWidget contents
        hamletToRepHtml [hamlet|
$doctype 5

<html>
    <head>
        <title>#{title}
        ^{headTags}
    <body>
        <div id="wrapper">
            ^{bodyTags}
|]
getRootR = defaultLayout $ do
    setTitle "Root test"
    toWidget [cassius|body
    color: red
|]
    toWidget [hamlet|<h1>Hello|]
main = warpDebug 4000 Layout
\end{lstlisting}%$

\section {getMessage}

Хотя мы ещё не рассматривали сессии, мне бы хотелось упомянуть здесь \lstinline!getMessage!. Обычным приёмом в веб-программировании является установка сообщения в одном обработчике и отображение его в другом. Например, если пользователь отправляет форму с помощью метода \lstinline!POST!, вы можете захотеть перенаправить его на другую страницу с сообщением <<Форма отправлена>>.

\begin{remark}
Такой сценарий известен как \href{http://en.wikipedia.org/wiki/Post/Redirect/Get}{Post/Redirect/Get}\footnotemark[\value{footnote}].
\end{remark}

\footnotetext{\href{http://en.wikipedia.org/wiki/Post/Redirect/Get}{\texttt{http://en.wikipedia.org/wiki/Post/Redirect/Get}}}

Чтобы облегчить реализацию этого подхода, в Yesod есть пара функций: \lstinline!setMessage!, устанавливающая сообщение в пользовательской сессии, и \lstinline!getMessage!, достающая это сообщение (и удаляющая его, чтобы оно не появилось второй раз). Рекомендуется размещать результат \lstinline!getMessage! в \lstinline!defaultLayout!. Например:

\begin{lstlisting}
    defaultLayout contents = do
        PageContent title headTags bodyTags <- widgetToPageContent contents
        mmsg <- getMessage
        hamletToRepHtml [hamlet|
$doctype 5

<html>
    <head>
        <title>#{title}
        ^{headTags}
    <body>
        $maybe msg <- mmsg
            <div #message>#{msg}
        ^{bodyTags}
|]
getRootR = defaultLayout [whamlet|<a href="@{MsgR}">message|]
getMsgR = setMessage "foo" >> redirect RootR >> return ()
main = warpDebug 4000 Layout
\end{lstlisting}%$

Мы рассмотрим \lstinline!getMessage!/\lstinline!setMessage! более детально, когда будем обсуждать \hyperref[chap:sessions]{сессии}.

\section {Нестандартные страницы сообщений об ошибках}

Одной из отличительных черт профессионального веб-сайта являются должным образом спроектированные страницы сообщений об ошибках. Yesod помогает вам в этом, автоматически используя ваш \lstinline!defaultLayout! для отображения страниц ошибок. Но иногда вам потребуется пойти чуть дальше. Для этого вам нужно переопределить метод \lstinline!errorHandler!:

\begin{lstlisting}
    errorHandler NotFound = fmap chooseRep $ defaultLayout $ do
        setTitle "Запрашиваемая страница не найдена"
        toWidget [hamlet|
<h1>Страница не найдена
<p>Просим прощения за неудобства, но запрашиваемая страница не может быть найдена.
|]
    errorHandler other = defaultErrorHandler other
getRootR = defaultLayout [whamlet|Hello World|]
main = warpDebug 4000 Layout
\end{lstlisting}

Здесь мы указываем нестандартную страницу для ошибки 404. Также мы можем использовать \lstinline!defaultErrorHandler!, если не хотим писать индивидуальный обработчик для каждого вида ошибки. Из-за ограничений типов нам приходится начинать определение методов с \lstinline!fmap chooseRep!, но в остальных отношениях вы можете писать обычную функцию-обработчик.

Фактически, вы даже можете возвращать специальные типы ответов, такие как перенаправления:

\begin{lstlisting}
    errorHandler NotFound = redirect RootR
    errorHandler other = defaultErrorHandler other
getRootR = defaultLayout [whamlet|Hello World|]
main = warpDebug 4000 Layout
\end{lstlisting}

\begin{remark}
Несмотря на то, что вы можете сделать это, я не рекомендую пользоваться таким приёмом. Ошибка 404 должна быть ошибкой 404.
\end{remark}

\section {Внешние CSS и Javascript}

\begin{remark}
Функциональность, описываемая в этом разделе, автоматически включается в сгенерированный шаблон сайта. Таким образом, вам самим не нужно беспокоиться о её реализации.
\end{remark}

Одним из наиболее мощных и устрашающих методов класса типов Yesod является \lstinline!addStaticContent!. Вспомним, что Widget состоит из нескольких компонентов, включая CSS и Javascript. Каким образом эти CSS/JS попадают в браузер пользователя? По умолчанию они выдаются в теге \lstinline!<head>! страницы: в тегах \lstinline!<style>! и \lstinline!<script>!, соответственно.

Такое решение является простым, но оно далеко от эффективного. Каждая загрузка страницы будет требовать загрузки всех CSS/JS <<с нуля>>, даже в том случае, когда ничего не изменилось! Что нам на самом деле хотелось бы, так это сохранить это содержимое во внешнем файле, а затем сослаться на него из HTML.

И в этом нам поможет \lstinline!addStaticContent!. Этот метод принимает три аргумента: расширение файла содержимого (\lstinline!css! или \lstinline!js!), MIME-тип содержимого (\lstinline!text/css! или \lstinline!text/javascript!) и само содержимое. А возвращает он один из трёх вариантов результата:

\begin{description}
  \item {Nothing}  \hfill \\
    Сохранение статического файла не выполнено; содержимое встраивается непосредственно в HTML. Это поведение по умолчанию.
  \item {Just (Left Text)} \hfill \\
    Содержимое было сохранено в виде отдельного файла, и используется заданная текстовая ссылка для указания на этот файл.
  \item {Just (Right (Route a, Query))} \hfill \\
    Аналогично, но используется типобезопасный URL с указанием параметров строки запроса.
\end{description}

Вариант с \lstinline!Left! полезен, если вы хотите сохранять статические файлы на внешнем сервере, например на CDN или сервере с большим объёмом памяти. Более часто используется вариант \lstinline!Right!, который хорошо связывается со статическим подсайтом\marginpar{речь, судя по всему про yesod-static, возможно, нужно примечание}. Это рекомендуемый подход для большинства приложений, и он используется в сгенерированном шаблонном сайте по умолчанию.

\begin{remark}
Возможно, вы спросите: если это рекомендуемый подход, то почему он не является вариантом по умолчанию? Проблема заключается в том, что он делает несколько предположений, которые не всегда соблюдаются: наличие в вашем приложении статического сайта, а также местоположение ваших статических файлов.
\end{remark}

Включённый в сгенерированный шаблон сайта \lstinline!addStaticContent! облегчает вам жизнь, выполняя ряд разумных действий:
\begin{itemize}
  \item Он автоматически сжимает ваш Javascript используя пакет \href{http://hackage.haskell.org/package/hjsmin}{hjsmin}\footnote{\href{http://hackage.haskell.org/package/hjsmin}{\texttt{http://hackage.haskell.org/package/hjsmin}}}
  \item Он именует выходные файлы на основе хэша содержимого файлов. Это значит, что вы можете устанавливать заголовки кэширования на дату в будущем, не боясь получить устаревшее содержимое.
  \item Также, так как имена файлов основаны на хэшах, вы можете быть уверены, что файл не потребуется перезаписывать поверх существующего. Сгенерированный шаблонный код автоматически проверяет существование такого файла и исключает затратные операции работы с диском, если операция записи не является необходимой.
\end{itemize}

% REVIEW Мне кажется, попытки дословного перевода "Smarter static files" обречены на корявость.
\section {Оптимизация использования статических файлов}

Google рекомендует использовать одну важную оптимизацию: \href{http://code.google.com/speed/page-speed/docs/request.html\#ServeFromCookielessDomain}{отдавать статические файлы с отдельного домена}\footnote{\href{http://code.google.com/speed/page-speed/docs/request.html\#ServeFromCookielessDomain}{\texttt{http://code.google.com/speed/page-speed/docs/request.html\#ServeFromCookielessDomain}}}. Преимущество такого подхода состоит в том, что куки, установленные для вашего домена, не будут отправляться при запросе статических файлов, позволяя сэкономить немного пропускной способности.

Для осуществления этого у нас есть метод \lstinline!urlRenderOverride!. Этот метод перехватывает обычный рендеринг URL и выставляет специальное значение для определённых маршрутов. Например, в сгенерированном шаблонном коде этот метод реализуется так:

\begin{lstlisting}
    urlRenderOverride y (StaticR s) =
        Just $ uncurry (joinPath y (Settings.staticRoot $ settings y)) $ renderRoute s
    urlRenderOverride _ _ = Nothing
\end{lstlisting}%$

Это означает, что статические маршруты обслуживаются со специального <<статического корня>>, в качестве значения которого вы можете указать другой домен при конфигурировании. Это является прекрасным примером мощности и гибкости типобезопасных URL: при помощи одной строки кода вы можете изменить рендеринг статических маршрутов во всех ваших обработчиках.

\section {Аутентификация/авторизация}

Для простых приложений проверка прав доступа в каждой функции-обработчике может быть простым и удобным решением. Однако такой подход не очень хорошо масштабируется. В конце концов вам захочется использовать более декларативный подход. Многие системы определяют свои списки контроля доступа, специальные файлы конфигурации и другие <<фокусы>>. В Yesod это делается при помощи старого доброго Haskell. Для этого используются следующие три метода:

\begin{description}
  \item {isWriteRequest} \hfill \\
    Определяет, является ли текущий запрос операцией чтения или записи. По умолчанию Yesod следует RESTful-принципам и предполагает, что запросы GET, HEAD, OPTIONS, и TRACE предназначены только для чтения, тогда как остальные могут выполнять запись.

  \item {isAuthorized} \hfill \\

    Этот метод принимает в качестве параметров маршрут (то есть, типобезопасный URL) и булево значение, указывающее на то, является ли запрос запросом записи. А возвращает он \lstinline!AuthResult!, который может быть одним из трёх значений:

    \begin{itemize}
      \item \lstinline!Authorized!
      \item \lstinline!AuthenticationRequired!
      \item \lstinline!Unauthorized!
    \end{itemize}

    По умолчанию он возвращает \lstinline!Authorized! для всех запросов.

  \item {authRoute} \hfill \\

    Если \lstinline!isAuthorized! возвращает \lstinline!AuthenticationRequired!, то метод выполняет перенаправление по указанному маршруту. Если маршрут не указан (поведение по умолчанию), возвращается ошибка 403 <<Permission Denied>>.

\end{description}

Эти методы прекрасно взаимодействуют с пакетом \href{http://hackage.haskell.org/package/yesod-auth}{yesod-auth}\footnote{\href{http://hackage.haskell.org/package/yesod-auth}{\texttt{http://hackage.haskell.org/package/yesod-auth}}}, который используется сгенерированным шаблонным сайтом, предоставляя целый набор вариантов аутентификации, таких как OpenID, BrowserID, электронная почта, имя пользователя и Twitter. Мы рассмотрим более конкретные примеры в \hyperref[chap:auth]{главе об аутентификации}.

\section {Некоторые простые настройки}

Не всё в классе типов Yesod является сложным. Некоторые методы представляют собой простые функции. Давайте пройдёмся по их списку.

\subsection {encryptKey}

Yesod использует сессии на стороне клиента, которые хранятся в зашифрованных, криптографически хэшированных куках. Но только если вы указали ключ шифрования. Если функция возвращает \lstinline!Nothing!, то сессии отключаются. Это может быть полезной оптимизацией для сайтов, которым не требуются сессии, так как это отключает пару операций шифрования/расшифрования для каждого запроса.

\begin{remark}
Комбинация шифрования и хэширования гарантирует два свойства: данные сессии защищены от несанкционированного доступа и непрозрачны. Шифрование без хэширования позволило бы пользователю произвольно менять содержимое куки, после чего они по-прежнему продолжили бы приниматься сервером, а хэширование без шифрования дало бы возможность просматривать данные.
\end{remark}

\subsection {clientSessionDuration}

Задаёт время, которое будет длиться сессия. По умолчанию~--- 2 часа.

\subsection {sessionIpAddress}

По умолчанию сессии привязаны к IP-адресу. Если ваши пользователи находятся за прокси-сервером, то это может привести к проблемам, если их IP вдруг изменится. Данная настройка позволяет отключить эту меру безопасности.

\subsection {cookiePath}

Задаёт путь в текущем домене, для которого устанавливаются куки. По умолчанию~--- <</>>, и это в большинстве случаев будет правильным вариантом. Исключением может быть сценарий, когда приложение работает на подпути в домене (как в примере с вики, приведённом ранее).

\subsection {maximumContentLength}

Чтобы предотвратить DoS-атаки\footnote{Denial of Service~--- отказ в обслуживании}, Yesod ограничивает максимальный размер тела запроса.  Иногда вам потребуется поднять этот максимум для некоторых маршрутов (к примеру для страниц загрузки файлов). Данный метод позволяет вам сделать это.

\subsection {yepnopeJs}

Здесь мы можете указать местоположение JavaScript-библиотеки \href{http://yepnopejs.com/}{yepnope}\footnote{\href{http://yepnopejs.com/}{\texttt{http://yepnopejs.com/}}}. Если это местоположение задано, то \lstinline!yepnope! будет асинхронно загружать JavaScript для ваших страниц.

\section {Выводы}

Класс типов Yesod включает в себя набор переопределяемых методов, которые позволяют конфигурировать ваше приложение. Все они не обязательны и имеют разумные реализации по умолчанию. Используя встроенные в Yesod конструкции, такие как \lstinline!defaultLayout! и \lstinline!getMessage!, вы получите единообразный внешний вид для всего вашего сайта, включая страницы, автоматически сгенерированные Yesod, такие как страницы сообщений об ошибках и страницы аутентификации.

Мы не рассмотрели полностью все методы класса типов Yesod в этой главе. Для просмотра полного списка доступных методов обратитесь к Haddock-документации.
