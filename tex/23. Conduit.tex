\chapter{Кондуиты}

Кондуиты используются для обработки потоков данных. Часто ленивые вычисления
позволяют обрабатывать большие объемы не загружая их в память целиком. Однако, 
использование такого подхода для ввода-вывода влечет требование ленивости ввода-вывода.
А его главный недостаток --- недетерминированность: у нас нет никаких гарантий когда
финализаторы наших ресурсов будут запущены. Для небольшого приложения это допустимо, но
для высоко нагруженного веб-сервера мы можем очень быстро исчерпать допустимые ресурсы,
например, дескрипторы для файлов.

Кондуиты позволяют оперировать большими потоками данных при детерминированном управлении
ресурсами. Они предоставляют унифицированный интерфес для потоков данных вне зависимости
от того откуда они поступают: из файлов, сокетов или памяти. В сочетании с ResourceT мы
можем безопасно работать с ресурсами, зная, что они будут гарантированно освобождены даже
в случае исключений.

В этом приложении рассматривается пакет conduit версии \verb=0.2=.


\section{Кондуиты в двух словах}

Хотя понимание низкоуровневой механики кондуитов рекомендуется, вы можете далеко
продвинуться и без неё. Давайте начнем  с нескольких  высокоуровневых 	 примеров.  Не
беспокойтесь, если некоторые детали      вам   будут       сейчас	  непонятны -- 
  мы разберем всё  в этом   приложении. Начнем с терминологии    и	  неско
льких	   примеров    кода.

\begin{itemize}
 \item \emph{Источник (source)} генерирует данные. Они могут быть в файле, прийти из
сокета или лежать списком в памяти. Мы обращаемся к этим данным забирая их из источника.
 \item \emph{Sink} потребляет данные. Основные примеры будут о функции суммирования
(сложения чисел из потока), файловом канале (пишет все пришедшие байты в файл) или
сокете. В конце обработки данных возвращается какое-то значение.
% FIXME: как sink на русский переводить?
 \item \emph{Кондуиты} преобразуют данные. В простейшем примере это будет функция
\lstinline=map=,
хотя бывает много других. Мы добавляем данные в кондуит также как и в sink. Но вместо
возврата одного значения, кондуит может вернуть несколько результатов каждый раз, когда в
него добавляются данные.
  \item \emph{Комбинирование (fuse)} --- термин Давида Мазьереса. Кондуит можно 
скомбинировать с источником данных (с помощью оператора \lstinline=$==) и получить
новый источник. Например, мы можем взять источник, читающий байты из файла, и
кондуит, преобразующий байты в текст. Скомбинировав их, мы получим 
источник, читающий текст из файла. Аналогично, кондуит и sink можно скомбинировать в sink
(оператор \verb#=$#), а два кондуита --- в новый кондуит (оператор \verb#=$=#).
  \item \emph{Соединение}. Мы можем присоединять источник к sink используя оператор
\verb=$$=.
Это приведет к тому, что данные будут передаваться из источника в sink до тех пор, пока
источник или sink не сообщат, что они <<закончили>.
\end{itemize}

Рассмотрим несколько примеров кода.
\begin{lstlisting}
{-# LANGUAGE OverloadedStrings #-}
import Data.Conduit -- the core library
import qualified Data.Conduit.List as CL -- some list-like functions
import qualified Data.Conduit.Binary as CB -- bytes
import qualified Data.Conduit.Text as CT

import Data.ByteString (ByteString)
import Data.Text (Text)
import qualified Data.Text as T
import Control.Monad.ST (runST)
\end{lstlisting}
Для начала соединим источник с sink. Будем использовать встроенные 
функции по работе с файлами для эффективности, константной памяти и ресурсо-безопасного
копирования файлов.

Обратите внимание: вначале мы используем \verb=$$= для соединения источник с sink, а
затем используем \lstinline=runResourceT=.
\begin{lstlisting}
copyFile :: FilePath -> FilePath -> IO ()
copyFile src dest = runResourceT $ CB.sourceFile src $$ CB.sinkFile dest
\end{lstlisting}
Модуль \lstinline=Data.Conduit.List= предоставляет некоторые число функций для создания
sink,
источников и кондуитов. Вот так выглядит свёртка: суммирование чисел.
\begin{lstlisting}
sumSink :: Resource m => Sink Int m Int
sumSink = CL.fold (+) 0
\end{lstlisting}
Мы можем реализовать то же самое более низкоуровнево, используя функцию \verb=sinkState=.
Они принимает три параметра: начальное состояние, функцию приема дополнительных данных и
функцию закрытия.
\begin{lstlisting}
sumSink2 :: Resource m => Sink Int m Int
sumSink2 = sinkState
    0 -- начальное значение
    -- обновим состояния согласно полученным данным 
    -- и сообщим что необходимы дополнительные данные
    (\accum i -> return $ StateProcessing (accum + i))
    (\accum -> return accum) -- вернуть текущее значение при закрытии
\end{lstlisting}
Другая полезная функция --- \verb=sourceList=. Скомбинировав её с нашей функций
\verb=sumSink=, мы получим встроенную реализацию функции sum.
\begin{lstlisting}
sum' :: [Int] -> Int
sum' input = runST $ runResourceT $ CL.sourceList input $$ sumSink
\end{lstlisting}
Поскольку это Haskell давайте создадим источник, который генерирует все числа Фибоначчи.
Для этого мы будем использовать \lstinline=sourceState=. Состояние будет содеражать
следующие два
число в последовательности. Также нам понадобится функция, которая вернет следующее число
и обновит состояние.
\begin{lstlisting}fibs :: Resource m => Source m Int
fibs = sourceState
    (0, 1) -- initial state
    (\(x, y) -> return $ StateOpen (y, x + y) x)
\end{lstlisting}
Посчитаем сумму первых 10 чисел Фибоначчи. Мы можем использовать изолированный кондуит,
чтобы быть уверенными, что sink суммирования приняла только 10 значений.
\begin{lstlisting}sumTenFibs :: Int
sumTenFibs =
       runST -- runs fine in pure code
     $ runResourceT
     $ fibs
    $= CL.isolate 10 -- fuse the source and conduit into a source
    $$ sumSink
\end{lstlisting}
Мы также можем скомбинировать кондуит и sink, поменяв местами некоторые операторы.
\begin{lstlisting}sumTenFibs2 :: Int
sumTenFibs2 =
       runST
     $ runResourceT
     $ fibs
    $$ CL.isolate 10
    =$ sumSink
\end{lstlisting}
Отлично, а теперь сделаем несколько кондуитов. Давайте преобразовывать числа в текст.
Кажется, функция map нам подойдет...
\begin{lstlisting}
intToText :: Int -> Text -- дополнительная функция 
intToText = T.pack . show

textify :: Resource m => Conduit Int m Text
textify = CL.map intToText
\end{lstlisting}
Аоспользуемся функцией \lstinline=conduitState= также как это было сделано выше. Здесь
нам не нужно
состояние, поэтому подставим фиктивное значение.
\begin{lstlisting}textify2 :: Resource m => Conduit Int m Text
textify2 = conduitState
    ()
    (\() input -> return $ StateProducing () [intToText input])
    (\() -> return [])
\end{lstlisting}
Сделаем кондуит unlines, который будет добавлять перевод строки в конце каждого блока
входных данных. Воспользуемся функцией \lstinline=CL.map=. feel free to write it with
conduitState as
well for practice.
\begin{lstlisting}unlines' :: Resource m => Conduit Text m Text
unlines' = CL.map $ \t -> t `T.append` "\n"
\end{lstlisting}
А теперь напишем функцию, которая печатает первые N чисел фибоначчи. Используем
кодировку UTF8.
\begin{lstlisting}writeFibs :: Int -> FilePath -> IO ()
writeFibs count dest =
      runResourceT
    $ fibs
   $= CL.isolate count
   $= textify
   $= unlines'
   $= CT.encode CT.utf8
   $$ CB.sinkFile dest
\end{lstlisting}
Мы использовали оператор \lstinline'$=', чтобы комбинировать кондуиты с источниками,
получая
новые источники. Можно делать и обратное: комбинировать кондуиты и sink. Можно даже
скомбинировать два кондуита.
\begin{lstlisting}
writeFibs2 :: Int -> FilePath -> IO ()
writeFibs2 count dest =
      runResourceT
    $ fibs
   $= CL.isolate count
   $= textify
   $$ unlines'
   =$ CT.encode CT.utf8
   =$ CB.sinkFile dest
\end{lstlisting}
Или мы можем скомбинировать все написанные кондуиты в один
\begin{lstlisting}
someIntLines :: ResourceThrow m -- encoding can throw an exception
             => Int
             -> Conduit Int m ByteString
someIntLines count =
      CL.isolate count
  =$= textify
  =$= unlines'
  =$= CT.encode CT.utf8
\end{lstlisting}
Теперь используем кондуит
\begin{lstlisting}
writeFibs3 :: Int -> FilePath -> IO ()
writeFibs3 count dest =
      runResourceT
    $ fibs
   $= someIntLines count
   $$ CB.sinkFile dest

main :: IO ()
main = do
    putStrLn $ "First ten fibs: " ++ show sumTenFibs
    writeFibs 20 "fibs.txt"
    copyFile "fibs.txt" "fibs2.txt"
\end{lstlisting}

\section{Структура главы.}

Остаток этой главы освещает следующие темы.
\begin{itemize}
 \item ResourceT --- техника, которая позволяет управлять освобождением ресурсов.
 \item Источники --- наши генераторы данных
 \item Sinks --- потребители данных
 \item Кондуиты --- преобразователи данных
 \item Буферизация --- техника борьбы с инверсией управления.
\end{itemize}

\section{Трансформер монады Resource}

Трансформер монады Resource (ResourceT) играет существенную роль в управлении ресурсами в
проектах, использующих кондуиты. Он поставляется вместе с библиотекй \verb=conduit=. Мы
будем рассматривать ResourceT самого по себе. Хотя некоторые решения в его дизайне
основаны на кондуитах, ResourceT можно использовато самого по себе.

\subsection{Назначение}

Что не так с этим кодом?
\begin{lstlisting}
import System.IO

main = do
    output <- openFile "output.txt" WriteMode
    input  <- openFile "input.txt"  ReadMode
    hGetContents input <<= hPutStr output
    hClose input
    hClose output
\end{lstlisting}

Если файл input.txt отсутствует, бросится исключение при попытке его открыть. В
результате hClose output никогда не будет вызвано и мы получим утечку важного ресурса
(дескриптора файлов). В небольшой программе это не критично, но очевидно, что мы не
сможем себе этого позволить в высоко нагруженном процессе сервера с большим аптаймом

К счастью проблема решается довольно просто:
\begin{lstlisting}
import System.IO

main =
    withFile "output.txt" WriteMode $ \output ->
    withFile "input.txt" ReadMode $ \input ->
    hGetContents input <<= hPutStr output
\end{lstlisting}

Использование withFile гшарантирует, что дескриптор будет закрыт, даже в случае
исключений. Оно также поддерживает асинхронные исключения. Вообще говоря, это прекрасный
подход для случаев, когда вы можете его использовать. Хотя withFile просто использовать,
часто он влечет переписывание всей программы. А это переписывание может быть очень
скучным и сильно неэффективным.

Возьмем, например, енумераторы. Если вы заглянете в документацию, то найдете функцию
enumFile для чте ния содержимого файла, но не найдете функции iterFile для записи
содержимого в файл. Так сделано потому, что поток управления итератов не позволяет
правильно управлять дескрипторами. Взамен, чтобо записать в файл вам надо создавать
дескриптор до запуска итерата, т.е.: 

\begin{lstlisting}
import System.IO
import Data.Enumerator
import Data.Enumerator.Binary

main =
    withFile "output.txt" WriteMode $ \output ->
    run_ $ enumFile "input.txt" $$ iterHandle output
\end{lstlisting}

Этот код работает хорошо, но представьте, что вместо простой передачи данных в файл, нам
надо провести длительное вычисление перед использование дескриптора. Мы будем иметь
дескриптор задолго до того, как он нам понадобится, блокируя важный ресурс нашего
приложения. Кроме этого, часто мы не можем открыть файл, так как мы поймем какой файл
надо открывать только тогда, когда прочитаем все данные.

Одна из заявленных целей кондуитов --- решить эту пролему с помощью ResourceT. Программа
выше может быть переписана вот так:
\begin{lstlisting}
{-# LANGUAGE OverloadedStrings #-}
import Data.Conduit
import Data.Conduit.Binary

main = runResourceT $ sourceFile "input.txt" $$ sinkFile "output.txt"
\end{lstlisting}

\subsection{Как это работает}

There are essentially three base functions on ResourceT, and then a bunch of
conveniences thrown on top. The first function is:
\begin{lstlisting}
register :: IO () -> ResourceT IO ReleaseKey
\end{lstlisting}
Полииморфность этой функции и остальных ниже не используется в полную силу, на самом деле
они могут работать с другими монадами кроме IO. In fact, almost any transformer on top of
IO, as well as any ST stacks, work. Мы разъясним детали познее.
% FIXME: Я провтыкал что такое ST
Эта функция региструет кусок кода, утверждения которого будут выполнены. Она также
возвращает ReleaseKey, которое используется в следующей функции:
\begin{lstlisting}release :: ReleaseKey -> ResourceT IO ()
\end{lstlisting}
Возоы функции \verb=release= от ReleaseKey немедленно выполняет действии, которое ыло
зарегистрировано ранее. Мы можем вызывать \verb=release= от одной и той же ReleaseKey
стольк раз, сколько пожелаем, при первом вызове регистрация действия отменяется. Это
значит, что вы можете безопасно зарегистрировать действие освобождения памяти, не
заботясь о том, что оно будет выполнено дважды.

Со временем, мы можем захотеть освободить ресурс, ResourceT. Чтобы сделать это,
воспользуемся:
\begin{lstlisting}
runResourceT :: ResourceT IO a -> IO a
\end{lstlisting}
В этой внешене невинной функции происходит вся магия. 
 It runs through ("пронизывать") all of
the
registered cleanup actions and performs them. Она безопасна с точки зрения исключений, в
том смысле, что освобождения ресурсов будут выполнены в случае и синхронных, и
асинхронныъ исключений. И, как было упомянуто выше, вызов функции release отменит
регистрацию действия. Таким образом нам не стоит беспокоиться о повторном освобождении
ресурсов.

Наконец, для удобства, мы приведем ещё одну функцию для выделения ресурса и регистрации
действия по его освобождению:
\begin{lstlisting}
with :: IO a -- ^ allocate
     -> (a -> IO ()) -- ^ free resource
     -> ResourceT IO (ReleaseKey, a)
\end{lstlisting}

Теперь перепишем с использованием ResourceT первый некачественный пример:
\begin{lstlisting}
import System.IO
import Control.Monad.Trans.Resource
import Control.Monad.Trans.Class (lift)

main = runResourceT $ do
    (releaseO, output) <- with (openFile "output.txt" WriteMode) hClose
    (releaseI, input)  <- with (openFile "input.txt"  ReadMode)  hClose
    lift $ hGetContents input <<= hPutStr output
    release releaseI
    release releaseO
\end{lstlisting}

Сейчас мы можем не беспокоиться об исключениях, затрудняющих освобождение ресурсов. Мы
можем опустить вызовы \verb=release= в таких маленьких программах как эта, это ни на что
не повлияет. Но в больших приложениях, где мы продолжим обработку дальше, этот код
гарантирует, что дескрипторы ресурсов освободятся как только это станет возможным, снижая
потребление ресурсов до минимума.

\subsection{Несколько слов о типах}

Как было упомянуто, ResourceT это нечто большее, чем код, исполняемый надо IO. Но давайте
опустим некторые вещи, которые нам потребуются от этой монады.
\begin{itemize}
\item Мутабельные ссылки, для хранения зарегистрированный действий по освобождению
ресурсов.
Нам может показаться, что мы можем использовать StateT трансформер, но тогда наше
состояние не сможет корректно обходиться с исключениями.
\item We only want to register actions in the base monad. For example, if we have a
ResourceT (WriterT [Int] IO) stack, we only want to register
IO actions. This makes it easy to lift our stacks around (i.e., add an extra
transformer to the middle of an existing stack), and avoids confusing issues about the
threading
of other monadic side-effects.
\item Some way to guarantee an action is performed, even in the presence of exceptions.
This
boils
down to needing a bracket-like function.
\end{itemize}

Для начала определим класс типов для монад, которые имеют мутабельные ссылки
\begin{lstlisting}
 class Monad m => HasRef m where
    type Ref m :: * -> *
    newRef' :: a -> m (Ref m a)
    readRef' :: Ref m a -> m a
    writeRef' :: Ref m a -> a -> m ()
    modifyRef' :: Ref m a -> (a -> (a, b)) -> m b
    mask :: ((forall a. m a -> m a) -> m b) -> m b
    mask_ :: m a -> m a
    try :: m a -> m (Either SomeException a)
\end{lstlisting}

We have an associated type to signify what the reference type should be. (For fans of
fundeps,
you'll see in the next section that this has to be an associated type.) Then we provide a
number of basic reference operations. В конце несколько функций для работы с
исключениями, которые необходимы для безопасной реализации функций, описанных в
последнем разделе. Реализация инстанса для IO допольно прямолинейна:
  
\begin{lstlisting}
instance HasRef IO where
    type Ref IO = I.IORef
    newRef' = I.newIORef
    modifyRef' = I.atomicModifyIORef
    readRef' = I.readIORef
    writeRef' = I.writeIORef
    mask = E.mask
    mask_ = E.mask_
    try = E.try 
\end{lstlisting}
Однако, при реализации инстанса монады ST мы сталкиваемся с проблемой: мы никак не можем
обрабатывать исключения в монаде ST. В результате, функции mask, mask\_ и try имеют
дефолтную реализацию без проверки исключений. Сейчас мы сформулируем первое
предупреждение:

\textit{Операции в монаде ST не безопасны относительно исключений. Мы не должен выделять
важные ресурсы в монаде ST когда используем ResourceT. You might be wondering why bother
with
ResourceT at all then for ST. The answer is that there is a
lot you can do with conduits without allocating scarce resources, and ST is a
great way to do this in a pure way. But more on this later. }

% FIXME: Что такое associated type?
Пункт номер 2: нам надо как-то работать с монадой Base. Опять же, мы можем
использовать 
associated type (почему опять --- будет сказано в следующем разделе). Наше решение будет
выгялдеть примерно так:
\begin{lstlisting}
class (HasRef (Base m), Monad m) => Resource m where
    type Base m :: * -> *

    resourceLiftBase :: Base m a -> m a 
\end{lstlisting}
Мы забыли о пункте 3 --- функции а-ля корзина. Нам понадобится ещё один метод в классе
типов:
\begin{lstlisting}
resourceBracket_ :: Base m a -> Base m b -> m c -> m c 
\end{lstlisting}
Причной, по которой перыве два аргумента функции resourceBracket\_ ``живут'' в монаде
Base,
является то, что в ResourceT все выделения и освобождения ресурсов, происходят в базовой
 монаде.

So on top of our HasRef instance for IO, we now need a
Resource instance as well. This is similarly straight-forward:
\begin{lstlisting}
 instance Resource IO where
    type Base IO = IO
    resourceLiftBase = id
    resourceBracket_ = E.bracket_
\end{lstlisting}

We have similar ST instances, with resourceBracket\_ having no
exception safety. The final step is dealing with monad transformers. We don't need to
provide a
HasRef instance, but we do need a Resource instance. The
tricky part is providing a valid implementation of resourceBracket\_. For this,
we use some functions from monad-control:

\begin{lstlisting}
instance (MonadTransControl t, Resource m, Monad (t m))
        => Resource (t m) where
    type Base (t m) = Base m

    resourceLiftBase = lift . resourceLiftBase
    resourceBracket_ a b c =
        control' \$ \run -> resourceBracket_ a b (run c)
      where
        control' f = liftWith f >>= restoreT . return 
\end{lstlisting}
For any transformer, its base is the base of its inner monad. Similarly, we lift to the
base by
lifting to the inner monad and then lifting to the base from there. The tricky part is the
implemetnation of \verb=resourceBracket_=. I will not go into a detailed explanation,
as I would simply make a fool of myself.

\subsection{Определение ResourceT}

Теперь у нас достаточно информации, чтобы осмыслить определение типа ResourceT:
\begin{lstlisting}
newtype ReleaseKey = ReleaseKey Int

type RefCount = Int
type NextKey = Int

data ReleaseMap base =
    ReleaseMap !NextKey !RefCount !(IntMap (base ()))

newtype ResourceT m a =
    ResourceT (Ref (Base m) (ReleaseMap (Base m)) -> m a)
\end{lstlisting}
Мы видим, что ReleaseKey это просто Int. Если вы загляните на несколько строк вниз, то
это определение обретет смысл, так как мы используем IntMap для того, чтобы хранить
зарегистрированые действия. Мы также определяем два синонима: RefCount и NextKey. NextKey
хранит последнее присвоенное значение ключа и инкрементируется каждый раз, когда
вызывается функция \verb=register=. Мы коснемся этого немного позже.

ReleaseMap хранит три вида информации: следующий ключ, счетчик ссылок, и затем map всех
зарегистрированных действий (actions). Заметьте, что ReleaseMap принимает типовый
параметр base, который определяет, какой монады действия по освобождению ресурсов  будут
использоваться 
% FIXME хрень какая-то. В оригинале which states which monad release actions must live
% in.

В конце концов, ResourceT является по существу ReaderT, который хранит мутабельную
ссылку на ReleaseMap. The reference type is determined by the base
of the monad in question, as is the cleanup monad. This is why we need to use associated
types.

Целью оставшейся части кода в модуле Control.Monad.Trans.Resource
является создание инстансов типа ResourceT.

\subsection{Другие классы типов}

Модуля предоставляет ещё три класса типов:
   
ResourceUnsafeIO

Любая монада может втянуть (lift) IO действия в себя, но это может быть небезопасно.
Первый пример этого --- ST. Care should be taken to
only lift actions which do not acquire scarce resources and which don't "fire the
missiles." Другими словами, все обычные предпреждения насчет unsafeIOToST имеют
справедлиы.
  
ResourceThrow

Для действий, которые могут бросить исключение.  Это автоматически приминимо ко всем
монадам на базе IO. Для ST-монад вы можете использовать трансформер ExceptionT, чтобы
предоставить возможности пробрасывания исключений. Это потребуется для некоторых функций
кондуитов, например, декодирование текста.
   
ResourceIO

Включает в себя несколько тайпклассов, в том числе два
упомянутых выше. Он создан только для удобства, мы может получить тот же результат без
него, потребуется только внимательнее оперировать типами (you'd just have to do a lot
more typing).
   
\subsection{Forking}

Может показаться, что ответвление от процесса хронически небезопасно при использовании 
ResourceT, так как родительский поток процесс может вызвать \verb=runResourceT= в то время
как дочерний использует ресурсы. Это, конечно же, так, если вы используете обычную
функцию forkIO.

Вообще говоря, вы не можете использовать стандартный forkIO, так как он использует монаду
IO, но вы можете использовать функцию fork из залифченой базовой монады.
По этой причине пакет regions не предоставляет экземпляр MonadBaseControl для этого
трансформатора (который очень похож на ResourceT). Однако, назначение ResourceT
не запретить программистам стрелять себе в ногу, а только, чтобы было легче писать
правильно. Поэтому, мы всё же предостовляем экземпляр, даже если он может быть
использован неправильно. Чтобы решить эту проблему ResourceT включает в себя
счетчик ссылок. Когда вы образовываете новый поток с помощью resourceForkIO,
значение RefCount в ReleaseMap инкрементируется. Всякий раз, когда runResourceT
вызывается, это значение декрементируется. Только когда оно достигент нуля происходят
действия по освобождению ресурсов.
 
\subsection{Дополнительные функции для удобства}

В дополнение мы расскажем про несколько функций, созданных для удобства.
%   
Функции \verb=newRef=, \verb=writeRef=, и \verb=readRef= оборачивают
функции с HasRef, позволяют им работать с любым ResourceT.
\verb=withIO= по сути является функцией \verb=with= с ограниченным типом, но она 
позволяет работать с некоторой вложенностью типов, в то время как при \verb=with=
пришлось бы продираться в глубину (but working around 
some of the nastiness with types you would otherwise run into). В общем, вам стоит
использовать withIO для написания IO кода.
\verb=transResourceT= позволит вам модифицировать в какой монаде исполняется ваш
\verb=ResourceT=, assuming it keeps the same
\begin{lstlisting}
    base.transResourceT :: (Base m ~ Base n)
               => (m a -> n a)
               -> ResourceT m a
               -> ResourceT n a
transResourceT f (ResourceT mx) = ResourceT (\r -> f (mx r))
\end{lstlisting}  

\section{Источники}

Проще всего понять что это такое посмотрев  на типы:
\begin{lstlisting}
data SourceResult m a = Open (Source m a) a | Closed
data Source m a = Source
    { sourcePull :: ResourceT m (SourceResult m a)
    , sourceClose :: ResourceT m ()
    }
\end{lstlisting}
Источник имеет две операции: вы можете запросить ещё данных и вы можете закрыть его (как,
например, закрыть дескриптор файла). При запросе новых данных вы или получаете немного
данных и новое  значение типа Source (источник остается открытым), или Nothing (источник
закрывается) . Давайте посмотрим на простеёшие примеры.
\begin{lstlisting}
  -- START
import Prelude hiding (repeat)
import Data.Conduit

-- | Never give any data
eof :: Monad m => Source m a
eof = Source
    { sourcePull = return Closed
    , sourceClose = return ()
    }

-- | Always give the same value
repeat :: Monad m => a -> Source m a
repeat a = Source
    { sourcePull = return $ Open (repeat a) a
    , sourceClose = return ()
    }
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}
Эти источники довольно прямолинейны, так как они всегда возвращают одно и то же. К тому
же их функции закрытия ничего не делают. Вам может показаться, что это баг: не должны ли 
мы возвращать в sourcePull \verb=return Closed= после того, как источник был закрыт? Это
не является необходимым, since one of the rules of sources is
that they can never be
reused. Другими словами:

    
\textit{Если источник вернул Open, то он предоставил вам новый источник, с которым и
стоит работать, заменив оригинальный. Если источник вернул Closed, тогда, вы больше не
может выполнять операции над ним.}
    
Не стоит очень беспокоиться по поводу сохранения этого инварианта. На практике, вам почти
не придется вызывать sourcePull или sourceClose самостоятельно. Также, навряд ли вам
придутся их самостоятельно описывать (для этого существуют sourceState и sourceIO).
%In fact, you hardly  even write them yourself either (that's what sourceState and
% sourceIO are for). FIXME уточнить это предожение
Идея в том, что мы можем сделать некоторые предположения, когда реализовываем источники. 
 

\subsection{Состояние}

В двух примерах источников выше есть кое-что общее: они всегда возвращают то же самое
значение. Другими словами, у них не состояния. Для более-менее серьёзных источников надо
потреуется реализовывать состояние.

Состояние может запросто быть определено вне нашей программы. Например, если мы
реализовываем источник, который читает данных из дескриптора, нам не нужно вручную
указывать никакого состояния, потому что дескриптор имеет состояние сам по себе.
Будем хранить состояние в источнике путём обновления возвращаемого значения типа Source в
конструкторе Open. Лучше всего это рассмотреть на примере.

\begin{lstlisting}
-- START
import Data.Conduit
import Control.Monad.Trans.Resource

-- | Provide data from the list, one element at a time.
sourceList :: Resource m => [a] -> Source m a
sourceList list = Source
    { sourcePull =
        case list of
            [] -> return Closed -- no more data

            -- This is where we store our state: by return a new
            -- Source with the rest of the list
            x:xs -> return $ Open (sourceList xs) x
        , sourceClose = return ()
        }
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}

Всякий раз, когда мы забираем данные из источника, он проверяет список. Если он пуст,
возвращаем Closed, что разумно. Если не пуст, возвращаем Open со следующим значением
списка и новым значением источника, вызванного от хвоста списка.

\subsection{sourceState and sourceIO}

В добавление к возможности манипулировать источниками, мы также имеет несколько функций,
позволяющих создавать источники более высокоуровнево. sourceState позволяет писать
код как будто вы используете монаду State. Вы предоставляете начальное состояние,
вункцию получения данных от текущего состояния, а она возвращает новое состояние и
новое значение. Перепишем sourceList с помощью неё:

\begin{lstlisting}
  -- START
import Data.Conduit
import Control.Monad.Trans.Resource

-- | Provide data from the list, one element at a time.
sourceList :: Resource m => [a] -> Source m a
sourceList state0 = sourceState
    state0
    pull
  where
    pull [] = return StateClosed
    pull (x:xs) = return $ StateOpen xs x
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}

Замечание по использованию конструкторов StateClosed и StateOpen. Они очень похожи на
Closed и Open, за исключением того, что при указании следующего источника вы указываете и
следующее состояние (остаток списка).

Другое распространенное применение --- аллокация ресурсов ввода-вывода (например,
открытие файла), регистрация функции свобождения ресурса (закрытие файла) и
предоставление функции получения данных из ресура.
В пакете conduit присутствует встроенная функция \verb=sourceFile=, которая выдает поток
значений типа ByteString. Давайте напишем совершенно неэффективную альтернативу,
которая будет возвразать поток символов.

\begin{lstlisting}
-- START
import Data.Conduit
import Control.Monad.Trans.Resource
import System.IO
import Control.Monad.IO.Class (liftIO)

sourceFile :: ResourceIO m => FilePath -> Source m Char
sourceFile fp = sourceIO
    (openFile fp ReadMode)
    hClose
    (\h -> liftIO $ do
        eof <- hIsEOF h
        if eof
            then return IOClosed
            else fmap IOOpen $ hGetChar h)
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}

Как и \verb=sourceState=, она использует конструкторы \verb*|Open| и \verb*|Closed|.
Функция sourceIO выполняет несколько действий  для нас:
\begin{itemize}
 \item Регистрирует функцию освобождения ресурса, с трансформерос ResourceT,
гарантируя, что она будет вызвана даже в случае исключений.
 \item Инициализирует поле sourceClose чтобы освободить ресурс немедленно.
 \item Как только вернется IOClosed, будет вызвано освобождение русурса.
\end{itemize}
  
\section{Sinks}

Они поглощают потоки данных и генерируют результат. Они должны всегда выдавать результат
и толтко единичный результат. Это зафиксированно в их типе.

Экземпляр Monad для sink упрощает композицию нескольких sink, в больший sink. Вы также
можете использовать встроенные функции для большинства своих нужд. Как и с источниками,
вам редко понадобиться погружаться в тонкости реалллизации. Начнем с примера:
получение строк и потока символов (будет предполагать наличие перевод строк в стиле Unix,
для простоты).

\begin{lstlisting}
import Data.Conduit
import qualified Data.Conduit.List as CL

-- Get a single line from the stream.
sinkLine :: Resource m => Sink Char m String
sinkLine = sinkState
    id -- initial state, nothing at the beginning of the line
    push
    close
  where
    -- On a new line, return the contents up until here
    push front '\n' =
        return $ StateDone Nothing $ front []

    -- Just another character, add it to the front and keep going
    push front char =
        return $ StateProcessing $ front . (char:)

    -- Got an EOF before hitting a newline, just give what we have so far
    close front = return $ front []

-- Get all the lines from the stream, until we hit a blank line or EOF.
sinkLines :: Resource m => Sink Char m [String]
sinkLines = do
    line <- sinkLine
    if null line
        then return []
        else do
            lines <- sinkLines
            return $ line : lines

content :: String
content = unlines
    [ "This is the first line."
    , "Here's the second."
    , ""
    , "After the blank."
    ]

main :: IO ()
main = do
    lines <- runResourceT $ CL.sourceList content $$ sinkLines
    mapM_ putStrLn lines
\end{lstlisting}
Running this sample produces the expected output:
\begin{verbatim}
This is the first line.
Here's the second.
\end{verbatim}
 
 
Функция sinkLine демонстрирует использование sinkState, которая очень похожа только что
виденную функцию sourceState. Она принимает три параметра: начальное состояние,
функцию ``добавления`` \verb=push= 
(принимает текущее состояние и входные данные и возвращает новое состояние и результат)
и функцию закрытия (принимает текущее состояние и возвращает вывод). Как
противоположность к sourceState, которая не нуждается в функции закрытия, sink должна
всегда возвращать результат.

В нашей функции push две клаузы. Когда она получет символ конца строки, процесс
заканчивается в состоянии StateDone. Nothing означает, что входных данных не
осталось (мы обсудим это позднее). Она также выдает все полученные символы. Вторая
клауза просто добавляет символ к текущему состоянию и соощает, что мы продолжаем работу
в состоянии StateProcessing. Функция close возвращает все символы.

Функция sinkLines демонстрирует как мы можем использовать монадический интерфейс,
чтобы создавать новые sinks. Если вы замените sinkLine на getLine, это будет выглядеть
как обычный код, который забирает строки и стандартного входного потока. This familiar
interface should make it easy to get up and running quickly.
%FIXME

\subsection{Типы}

Типы для синков несколько более хитроумны, чем для источников. Давайте взглянем на них:

\begin{lstlisting}
type SinkPush input m output = input -> ResourceT m (SinkResult input m output)
type SinkClose m output = ResourceT m output

data SinkResult input m output =
    Processing (SinkPush input m output) (SinkClose m output)
  | Done (Maybe input) output

data Sink input m output =
    SinkNoData output
  | SinkData
        { sinkPush :: SinkPush input m output
        , sinkClose :: SinkClose m output
        }
  | SinkLift (ResourceT m (Sink input m output))
\end{lstlisting}  
% Whenever a sink is pushed to, it can either say it needs more data
% (Processing) or say it's all done. When still processing, it must provided
% updated push and close function; when done, it returns any leftover inut and the output.
% Fairly
% straight-forward.
% 
% The first real "gotcha" is the three constructors for Sink. Why do we need
% SinkNoData: aren't sinks all about consuming data? The answer is that we need
% it to efficiently implement our Monad instance. When we use
% return, we're giving back a value that requires no data in order to compute it.
% We could model this with the SinkData constructor, with something like:
% \begin{lstlisting}
% myReturn a = SinkData (\input -> return (Done (Just input) a)) (return a)
% \end{lstlisting}
% But doing so would force reading in an extra bit of input that we don't need right now,
% and
% possibly will never need. (Have a look again at the sinkLines example.) So
% instead, we have an extra constructor to indicate that no input is required. Likewise,
% SinkLift is provided in order to implement an efficient
% MonadTrans instance.
% 
\subsection{Sinks: no helpers}
% Let's try to implement some sinks on the "bare metal", without any helper functions.
% \begin{lstlisting}
%   -- START
% import Data.Conduit
% import System.IO
% import Control.Monad.Trans.Resource
% import Control.Monad.IO.Class (liftIO)
% 
% -- Consume all input and discard it.
% sinkNull :: Resource m => Sink a m ()
% sinkNull =
%     SinkData push close
%   where
%     push _ignored = return $ Processing push close
%     close = return ()
% 
% -- Let's stream characters to a file. Here we do need some kind of
% -- initialization. We do this by initializing in a push function,
% -- and then returning a different push function for subsequent
% -- calls. By using withIO, we know that the handle will be closed even
% -- if there's an exception.
% sinkFile :: ResourceIO m => FilePath -> Sink Char m ()
% sinkFile fp =
%     SinkData pushInit closeInit
%   where
%     pushInit char = do
%         (releaseKey, handle) <- withIO (openFile fp WriteMode) hClose
%         push releaseKey handle char
%     closeInit = do
%         -- Never opened a file, so nothing to do here
%         return ()
% 
%     push releaseKey handle char = do
%         liftIO $ hPutChar handle char
%         return $ Processing (push releaseKey handle) (close releaseKey handle)
% 
%     close releaseKey _ = do
%         -- Close the file handle as soon as possible.
%         return ()
% 
% -- And we'll count how many values were in the stream.
% count :: Resource m => Sink a m Int
% count =
%     SinkData (push 0) (close 0)
%   where
%     push count _ignored =
%         return $ Processing (push count') (close count')
%       where
%         count' = count + 1
% 
%     close count = return count
% -- STOP
% main :: IO ()
% main = return ()
% \end{lstlisting}
% Nothing is particularly complicated to implement. You should notice a common pattern
here:
% declaring your push and close functions in a where clause, and then
% using them twice: once for the initial SinkData, and once for the
% Processing constructor. This can become a bit tedious; that's why
% we have helper functions.
% 
\subsection{Sinks: with helpers}
% Let's rewrite sinkFile and count to take advantage of the
% helper functions sinkIO and sinkState, respectively.
% \begin{lstlisting}
%   -- START
% import Data.Conduit
% import System.IO
% import Control.Monad.IO.Class (liftIO)
% 
% -- We never have to touch the release key directly, sinkIO automatically
% -- releases our resource as soon as we return IODone from our push function,
% -- or sinkClose is called.
% sinkFile :: ResourceIO m => FilePath -> Sink Char m ()
% sinkFile fp = sinkIO
%     (openFile fp WriteMode)
%     hClose
%     -- push: notice that we are given the handle and the input
%     (\handle char -> do
%         liftIO $ hPutChar handle char
%         return IOProcessing)
%     -- close: we're also given the handle, but we don't use it
%     (\_handle -> return ())
% 
% -- And we'll count how many values were in the stream.
% count :: Resource m => Sink a m Int
% count = sinkState
%     0
%     -- The push function gets both the current state and the next input...
%     (\state _ignored -<
%         -- and it returns the new state
%         return $ StateProcessing $ state + 1)
%     -- The close function gets the final state and returns the output.
%     (\state -> return state)
% -- STOP
% main :: IO ()
% main = return ()
% \end{lstlisting}
% Nothing dramatic, just slightly shorter, less error-prone code. Using these two helper
% functions is highly recommended, as it ensures proper resource management and state
% updating.
%
\subsection{Функции работы со списками}

Можно легко писать собственные sinks, если вы пользуетесь встроенными примитивами из
модуля \verb*|Data.Conduit.List|. Там есть аналоги для типичных функций работы со
списками, например, свертки. (Там так же есть некоторые кондуиты,
например, conduit:Data.Conduit.List:map.)

Если вы желаете поупражняться в кондуитах, то реализация функций из модуля LIst (с
хэлперами и без) будет для начала неплохо.

Давайте сначала посмотрим на простые функции, которые можно подружить со встроенными
синками.
\begin{lstlisting}
-- START
import Data.Conduit
import qualified Data.Conduit.List as CL
import Control.Monad.IO.Class (liftIO)

-- A sum function.
sum' :: Resource m => Sink Int m Int
sum' = CL.fold (+) 0

-- Print every input value to standard output.
printer :: (Show a, ResourceIO m) => Sink a m ()
printer = CL.mapM_ (liftIO . print)

-- Sum up all the values in a stream after the first five.
sumSkipFive :: Resource m => Sink Int m Int
sumSkipFive = do
    CL.drop 5
    CL.fold (+) 0

-- Print each input number and sum the total
printSum :: ResourceIO m => Sink Int m Int
printSum = do
    total <- CL.foldM go 0
    liftIO $ putStrLn $ "Sum: " ++ show total
    return total
  where
    go accum int = do
        liftIO $ putStrLn $ "New input: " ++ show int
        return $ accum + int
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}

\subsection{Соединение}

Наконец, мы хотим как-то поиспользовать наши синки. Пока мы умеет только вручную вызвать
\verb*|sinkPush| and \verb*|sinkClose|, но эт утомительно. Пример:
\begin{lstlisting}
import Data.Conduit
import Control.Monad.IO.Class (liftIO)

printSum :: Sink Int m Int
printSum = undefined
-- START
main :: IO ()
main = runResourceT $ do
    res <-
        case printSum of
            SinkData push close -> loop [1..10] push close
            SinkNoData res -> return res
    liftIO $ putStrLn $ "Got a result: " ++ show res
  where
    loop [] _push close = close
    loop (x:xs) push close = do
        mres <- push x
        case mres of
            Done _leftover res -> return res
            Processing push' close' -> loop xs push' close'
\end{lstlisting}
Вместо этого, рекомендуется соединять ваш синк с источником. Это проще, 
ошибок будет меньше, да и вас будет больше простора в том, откуда приходят ваши данные.
Перепишем пример выше:
\begin{lstlisting}
import Data.Conduit
import qualified Data.Conduit.List as CL
import Control.Monad.IO.Class (liftIO)

printSum :: Sink Int m Int
printSum = undefined
-- START
main :: IO ()
main = runResourceT $ do
    res <- CL.sourceList [1..10] $$ printSum
    liftIO $ putStrLn $ "Got a result: " ++ show res
\end{lstlisting}

Соединение требует проверки конструктора синка (SinkData vs. 
SinkNoData vs. SinkLift), запроса данных из источника , их передачи в синк и его закрытия.

Однако, есть один момент, который я хотел подчеркнуить таким длинным примером. Со второй
по последнюю строчки мы игнорируем значение, возвращаемое с Done. Это вносит проблему
потери данных. Это важная тема, которую стоило бы хорошенько обсудить. К сожалению,
сейчас мы не сможем полностью её осветить, так как мы не обсудили главного виновника
драмы: Conduits (the type, not the package). % FIXME добавить ссылку

Но если говорить кратко, то оставшееся значение не всегда игнорируется. Экземпляр Monad,
например, использует его для передачи данных из одной синки в другую по цепочке.
В действительности, настоящий оператор соединения не всегде отбрасывает остатки. Когда мы
обсудим источники, получение данных из которых можно приостанавливать (resumable), мы
увидим, что оставшееся значение складывается обратно в буфер, чтобы позволить последующим
синкам переиспользовать существующие источники для получения данных.

\section{Кондуиты}

В этой части мы рассмотрим главный тип данных в нашем пакете --- кондуиты. В то время как
источники генерируют данные а синки их поглощают, кондуиты преобразуют поток.

\subsection{Типы}

Также как мы и делали ранее, начнем с изучения используемых типов.
\begin{lstlisting}
data ConduitResult input m output =
    Producing (Conduit input m output) [output]
  | Finished (Maybe input) [output]

data Conduit input m output = Conduit
    { conduitPush :: input -> ResourceT m (ConduitResult input m output)
    , conduitClose :: ResourceT m [output]
    }
\end{lstlisting}
Это очень похоже на то, что мы видели с синками. В кондуит можно положить данные и в этом
случае он вернет результат. Этот результат демонстрирует либо, что данные ещё
генерируются, либо, что работа закончена. Если кондуит закрыт, то он возвращает некоторый
результат.


But let's examine the idiosyncracies a bit. Like sinks, we can only push one piece of
input at
a time, and leftover data may be 0 or 1 pieces. However, there are a few changes:

  
When producing (the equivalent of processing for a sink), we can return output. This is
because a conduit will product a new stream of output instead of producing a single
output
value
at the end of processing.
A sink always returns a single output value, while a conduit returns 0 or more outputs
(a
list). To understand why, consider conduits such as concatMap (produces
multiple outputs for one input) and filter (returns 0 or 1 output for each
input).
We have no special constructor like SinkNoData. That's because we provide no
Monad instance for conduits. We'll see later how you can still use a familiar
Monadic approach to creating conduits.
  
Overall conduits should seem very similar to what we've covered so far.

\subsection{Простые кондуиты}

Начнем с определения простейших кондуитов без состояния.
\begin{lstlisting}
  -- START
import Prelude hiding (map, concatMap)
import Data.Conduit

-- A simple conduit that just passes on the data as-is.
passThrough :: Monad m => Conduit input m input
passThrough = Conduit
    { conduitPush = \input -> return $ Producing passThrough [input]
    , conduitClose = return []
    }

-- map values in a stream
map :: Monad m => (input -> output) -> Conduit input m output
map f = Conduit
    { conduitPush = \input -> return $ Producing (map f) [f input]
    , conduitClose = return []
    }

-- map and concatenate
concatMap :: Monad m => (input -> [output]) -> Conduit input m output
concatMap f = Conduit
    { conduitPush = \input -> return $ Producing (concatMap f) $ f input
    , conduitClose = return []
    }
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}

\subsection{Кондуиты с состоянием}
%
Конечно же, не все кодуиты могут быть объявлены без состояния.  Реализация (кондуитов с состоянием) в лоб (bare metal) не очень сложна.
\begin{lstlisting}
  -- START
import Prelude hiding (reverse)
import qualified Data.List
import Data.Conduit
import Control.Monad.Trans.Resource

-- Reverse the elements in the stream. Note that this has the same downside as
-- the standard reverse function: you have to read the entire stream into
-- memory before producing any output.
reverse :: Resource m => Conduit input m input
reverse =
    mkConduit []
  where
    mkConduit state = Conduit (push state) (close state)
    push state input = return $ Producing (mkConduit $ input : state) []
    close state = return state

-- Same thing with sort: it will pull everything into memory
sort :: (Ord input, Resource m) => Conduit input m input
sort =
    mkConduit []
  where
    mkConduit state = Conduit (push state) (close state)
    push state input = return $ Producing (mkConduit $ input : state) []
    close state = return $ Data.List.sort state
-- STOP
main :: IO ()
main = return ()
\end{lstlisting} 
Но мы можем сделать лучше. Как и в случае с sourceState и sinkState, мы с помощью conduitState кое-что упростить.
\begin{lstlisting}
  -- START
import Prelude hiding (reverse)
import qualified Data.List
import Data.Conduit

-- Reverse the elements in the stream. Note that this has the same downside as
-- the standard reverse function: you have to read the entire stream into
-- memory before producing any output.
reverse :: Resource m => Conduit input m input
reverse =
    conduitState [] push close
  where
    push state input = return $ StateProducing (input : state) []
    close state = return state

-- Same thing with sort: it will pull everything into memory
sort :: (Ord input, Resource m) => Conduit input m input
sort =
    conduitState [] push close
  where
    push state input = return $ StateProducing (input : state) []
    close state = return $ Data.List.sort state
-- STOP
main :: IO ()
main = return ()
\end{lstlisting}
\subsection{Использование кондуитов}

Кондуиты работаюь с другими сущностями этого пакета с помощью комбинирования (fusing).
Кондуит может быть скомбинирован с источником, создавая новый источник, с синком для получения нового синка, или с другим кондуитом, для получения новго кондуита. Неплохо бы взглянуть на операторы комбинирования. 
\begin{lstlisting}
  -- Left fusion: source + conduit = source
($=) :: (Resource m, IsSource src) => src m a -> Conduit a m b -> Source m b

-- Right fusion: conduit + sink = sink
(=$) :: Resource m => Conduit a m b -> Sink b m c -> Sink a m c

-- Middle fusion: conduit + conduit = conduit
(=$=) :: Resource m => Conduit a m b -> Conduit b m c -> Conduit a m c
\end{lstlisting}
Использование этих операторов довольно прямолинейно.
% FIXME: что это нахрен за два доллара?
\begin{lstlisting}
  useConduits = do
    runResourceT
          $  CL.sourceList [1..10]
          $= reverse
          $= CL.map show
          $$ CL.consume

    -- equivalent to
    runResourceT
          $  CL.sourceList [1..10]
          $$ reverse
          =$ CL.map show
          =$ CL.consume

    -- and equivalent to
    runResourceT
          $  CL.sourceList [1..10]
          $$ (reverse =$= CL.map show)
          =$ CL.consume
\end{lstlisting}
Существует ещё один способ выразить то же самое. Его поиск оставлен читателя в качестве упражнения.

Вам может показаться, что такое количество различных способов комбинирования чрезмерно. Хотя вы можете выбирать какой способ вам больше нравится, в большинстве случаем вам нужен определенный способ. Например:

\begin{itemize}   
\item Если у вас поток чисел и вы хотите применить кондуит (например, map show) только к некоторой части потока, которая будет переданна в определенную синку, то нужно использовать оператор комбинирования справа.
\item Если вы читаете файл и хотите распарсить его целиком как текстовые данные, стоит использовать оператор комбинирования слева, чтобы преоразовать весь поток.
\item Если вам нужны переиспользуемые кондуиты, которые будут объединяться в большие кондуиты, используете срединное комбинирование.
\end{itemize}

\subsection{Потери данных}
 
Забудем о кондуитах на минутку. Давайте напишем программу, которая использует голые списки: она примет список чисел, применит некоторое преобразование к нему, возьмет первые 5 элементов и сделает с ними что-то, а потом возьмем оставшиеся непреобразованные денные и сделает с ними что-нибудь ещё. Как-то так, например:
\begin{lstlisting}
  main = do
    let list = [1..10]
        transformed = map show list
        (begin, end) = splitAt 5 transformed
        untransformed = map read end
    mapM_ putStrLn begin
    print $ sum untransformed
\end{lstlisting}
Но ясно, что это не очень хорошее решение в целом, потому что нам не нужно преобразовывать в одну сторону, а потом обратно все элементы списка. К тому же мы не всегда будем иметь функцию обратного преобразования. Другая причина --- неэффективность. В данном случае мы можем написать более эффективное решение:
\begin{lstlisting}
  main = do
    let list = [1..10]
        (begin, end) = splitAt 5 list
        transformed = map show begin
    mapM_ putStrLn transformed
    print $ sum end
\end{lstlisting}
Обратите внимание: мы разбиваем список до применения  нашего преобразования. Этот подход работает, потому что при использовании map у нас есть взаимооднозначное соответствие между элементами. Поэтому выделение первых пяти элементов до или после преобразования суть одно и то же. Но что будет, если мы заменим map show чем-то более сложное.
\begin{lstlisting}
  deviousTransform =
    concatMap go
  where
    go 1 = [show 1]
    go 2 = [show 2, "two"]
    go 3 = replicate 5 "three"
    go x = [show x]
\end{lstlisting}
Теперь нет взаимооднозначного соответствия. Следовательно, мы не можем использовать второй способ. Но на самом деле всё гораздо хуже: мы также не можем использовать первый метод, потому что у нас нет обратного преобразования к функции deviousTransform.
 
Я знаю только одно решение проблемы: преобразовавать значения по одному. Конечный вариант будет выглядеть примерно так:
\begin{lstlisting}
deviousTransform 1 = [show 1]
deviousTransform 2 = [show 2, "two"]
deviousTransform 3 = replicate 5 "three"
deviousTransform x = [show x]

transform5 :: [Int] -> ([String], [Int])
transform5 list =
    go [] list
  where
    go output (x:xs)
        | newLen <= 5 = (take 5 output', xs)
        | otherwise = go output' xs
      where
        output' = output ++ deviousTransform x
        newLen = length output'

    -- Degenerate case: not enough input to make 5 outputs
    go output [] = (output, [])

main = do
    let list = [1..10]
        (begin, end) = transform5 list
    mapM_ putStrLn begin
    print $ sum end
\end{lstlisting}    
Результат работы программы будет таким:
\begin{verbatim}
1
2
two
three
three
49
\end{verbatim}
Стоит обратить внимание, что число 3 преобразуется в пять копий слова "three", хотя только две из них попадают в результат. Остаток отбрасывается при вызове \verb=take 5=.
 
Этот пример наглядно демонстрирует проблему потери данных при использовании кондуитов. Заставляя кодуиты принимать только одни данные в текущий момент времени мы избегаем проблемы немедленного преобразования слишком большого количества данных. Это не значит, что мы не теряем данные: если кондуит, генерирует слишком много данных, что синк не может их все принять, то некоторая ихз часть потеряется.
 
В оправдание скажем, что кондуиты позволяют разбивать данные на куски (chunking), что бы избегать потерь данных.
% To put all this another way: conduits avoid chunking to get away from data loss. 
Эта проблема встречается не только при использовании кондуитов. если вы взглянете на реализацию concatMapM для енумераторов, вы увидете что там элементы обрабатываются по одному. В кондуитах мы выбираем решать эту проблему на уровне типов ( In conduits, we opted to force the issue at the type level).
 
\subsection{SequencedSink}
 
Предположим, что нам необходимо скомбинировать существующие кондуиты  и  синки, чтобы получить новый, более сложный кондуит. Например, мы хотим написать кондуит, который прнимает поток чисел и суммирует их по пять. Другими словами, для входа [1..50] он должен вернуть [15,40,65,90,115,140,165,190,215,240]. Мы определенно можем это сделать с помощью нихкоуровнего интерфеса кондуитов.
\begin{lstlisting}
 import Data.Conduit
 -- START
 sum5Raw :: Resource m => Conduit Int m Int
 sum5Raw =
     conduitState (0, 0) push close
   where
     push (total, count) input
         | newCount == 5 = return $ StateProducing (0, 0) [newTotal]
         | otherwise     = return $ StateProducing (newTotal, newCount) []
       where
         newTotal = total + input
         newCount = count + 1
     close (total, count)
         | count == 0 = return []
         | otherwise  = return [total]
 -- STOP
 main :: IO ()
 main = return ()
\end{lstlisting}
Но это неудобно, так как мы уже имеем всё, что нужно, чтобы реализовать это высокоуровнево. У нас есть синк свертки, чтобы складывать числа и изолирующий кондуит, который позволит передать в синк только определенное количество данных. Может быть мы сможем их скомбинировать?
 
Нам нужен SequencedSink. Он является обычным синком, за исключением того, что он возвращает специальный SequencedSinkResponse. Это значение может вернуть новый результат, остановить обработку данных или передать управление в новый кондуит. (Дополнительную информаци вы можете почерпнуть в Haddocks.) Потом мы сможем преобразовать это в кондуит с помощью функции sequenceSink. Она также принимает состояние, которое будет передано в синк.
 
 Теперь мы можем переписать sum5Raw более высокоуровнево.
 \begin{lstlisting}
 import Data.Conduit
 import qualified Data.Conduit.List as CL
 -- START
 sum5 :: Resource m => Conduit Int m Int
 sum5 = sequenceSink () $ \() -> do
     nextSum <- CL.isolate 5 =$ CL.fold (+) 0
     return $ Emit () [nextSum]
 -- STOP
 main = return ()
 \end{lstlisting}
Здесь все () --- это  неиспользуемое состояние, которое передается и там игнорируется. Другими словами мы сделали ровно то, что хотели. Мы скомбинировали изолирование со сверткой, чтобы получить сумму следующих пять элементов потока. Затем мы возвращаем значение, чтобы начать всё сначала.
 
Предполжим, что мы хотим это слегка изменить. Мы хотим получить первые 8 сумм и затем вернуть эти значения, увеличенные в двое. Будем хранить сколько значений мы вернули в состоянии, а затем воспользуемся конструктором StartConduit, чтобы передать управление кондуиту, умножающему на 2.
\begin{lstlisting}
 import Data.Conduit
 import qualified Data.Conduit.List as CL
 -- START
 sum5Pass :: Resource m => Conduit Int m Int
 sum5Pass = sequenceSink 0 $ \count -> do
     if count == 8
         then return $ StartConduit $ CL.map (* 2)
         else do
             nextSum <- CL.isolate 5 =$ CL.fold (+) 0
             return $ Emit (count + 1) [nextSum]
 -- STOP
 main = return ()
\end{lstlisting}
Очевидно, что примеры выше несколько искусственны, но я надеюсь, что прояснили мощь и простоту этого подхода. 
 
\section{Буферизация}

Буферизация --- это одно из уникальных свойств кондуитов. С её использованием вам больше не придется заботиться о потоке управления в вашей программе.
% With buffering, conduits no longer need to control the flow of your application. 
Иногда, она поможет упростить код.


Инверсия управления.

Буферизация была одной из главный причин для создания кондуитов. Чтобы понять её важность, расмотрим подход, который мы видели не так давно, который приведет к инверсии управления.

Инверсия управления в разных контекстах может означать разное. Если вы возражаете против использования этого термна здесь, то можете называть это "тёплой пушистой штуковиной" --- я не буду возражать.

Предположим, что вы хотите посчитать, как много переводов строки в файле. При стандартном императивном подходе вы бы действовали примерно так:

\begin{itemize}  
\item   Открываем файл
\item   Читаем данные в буфер
\item   Обходим данные в буфере, икрементируя счетчик, когда встречаем перевод строки.
\item   Возвращаемся на шаг два
\item   Закрываем файл.
\end{itemize}
Обратите внимание, что ваш код явно вызывает другой код, и этот джругой код передает управление обратно в ваш код. Вы полностью контролируете поток управления вашей программы. В кондуитах, как мы видели, так сделать не получится. Вместо этого мы будем:
\begin{itemize}
\item  писать в синк, который считает переводы строки и инкрементирует аккумулятор
\item  Соединять синк с источником.
\end{itemize} 
У меня нет сомнений, что этот подход проще. Нам не придется заботиться об открытии и закрытии файла и чтения данных из него. Здесь необходимые данные просто предоставляются нам. Это преимущество инверсии управления --- мы можем сосредоточиться именно на нашей части кода.

Мы используем этот подход всюду в хаскеле: например, вместо readMVar и putMVar, мы используетм withMVar. Не используем openFile и closeFile, сразу пишем withFile и передаем функцию, которая использует дескриптор. Даже в С используется такое же: зачем malloc и free когда можно писать сразу же alloca?

Вообще-то, мы уже далеко ушли от темы. Конечно же, мы не можем использовать alloca везде. Функция alloca выделять память только на локальном стеке, а не в динамически в куче. Никак нельзя вернуть выделенную память наружу из данной функции.

На самом деле, некоторые ограничения применяются ко всему семейству таких функций: мы никогда не сможем вернуть выделенный ресурс наружу из данного "блока" кода. Обычно, это и не нужно, просто мы должны осмыслить, что это изменение повлияет на структуры наших программ. Однако, в сложных окружениях становится сложно управлять этим, а порою просто не возможно. 
% FIXME: Что-то я тут не совсем понял.
% Usually this works
% out just fine, but we need to recognize that this is a change in how we structure our
% programs. Often times, with simple examples, this is a minor change. However, in larger
% settings this can become very difficult to manage, bordering on impossible at times.

\subsection{Веб-сервер}

Давайте напишем веб-сервер. Нам понадобятся следующие низкоуровневые операции:
\begin{lstlisting}
data Socket
recv    :: Socket -> Int -> IO ByteString -- returns empty when the socket is closed
sendAll :: Socket -> ByteString -> IO ()
\end{lstlisting}
Также нам понадобится функция handleConn, которая будет обрабатывать соединения по одному. Например такая:
\begin{lstlisting}
data Request  -- request headers, HTTP version, etc
data Response -- status code, response headers, resposne body
type Application = Request -> IO Response
handleConn :: Application -> Socket -> IO ()
\end{lstlisting}
Что наша функция handleConn должна делать? Если кратко, то:
\begin{itemize}  
\item   Разобрать тело запроса
\item   Разобрать заголовки запроса
\item   Создать значение типа Request
\item   Передать его приложению, чтобы получить обратно ответ типа Response
\item   Передать ответ обратно в сокет.
\end{itemize}  
Начнем с реализации пунктов 1 и 2 вручную, без кондуитов. Будем действовать очень просто, предполагая, что запрос состоит из трех слов, разделенных пробелами. Остановимся на таком варианте:
\begin{lstlisting}
data RequestLine = RequestLine ByteString ByteString ByteString

parseRequestLine :: Socket -> IO RequestLine
parseRequestLine socket = do
    bs <- recv socket 4096
    let (method:path:version:ignored) = S8.words bs
    return $ RequestLine method path version
\end{lstlisting}
Здесь две проблемы: мы не рассматриваем случай, когда в чанке данных меньше трех строк и отбрасываем лишние данные. Мы определенно можем решить обе проблемы вручную, но это будет утомительно. Гораздо проще переписать это в териминах кондуитов.
\begin{lstlisting}
-- START
import Data.ByteString (ByteString)
import qualified Data.ByteString as S
import Data.Conduit
import qualified Data.Conduit.Binary as CB
import qualified Data.Conduit.List as CL

data RequestLine = RequestLine ByteString ByteString ByteString

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = do
    let space = toEnum $ fromEnum ' '
    let getWord = do
            CB.dropWhile (== space)
            bss <- CB.takeWhile (/= space) =$ CL.consume
            return $ S.concat bss

    method <- getWord
    path <- getWord
    version <- getWord
    return $ RequestLine method path version
-- STOP
main = return ()
\end{lstlisting}
Это значит, что нашему коду будут предоставлены данные, как только они поступят и дополнительные данные будут автоматически забуферизованы в источнике, готовые к использованию. Теперь мы можем легко состыковать наши программы вместе, пользуясь мощью подхода на кондуитах:
\begin{lstlisting}
  -- START
import Data.ByteString (ByteString)
import Data.Conduit
import Data.Conduit.Network (sourceSocket)
import Control.Monad.IO.Class (liftIO)
import Network.Socket (Socket)

data RequestLine = RequestLine ByteString ByteString ByteString
type Headers = [(ByteString, ByteString)]
data Request = Request RequestLine Headers
data Response = Response
type Application = Request -> IO Response

parseRequestHeaders :: Sink ByteString IO Headers
parseRequestHeaders = undefined

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = undefined

sendResponse :: Socket -> Response -> IO ()
sendResponse = undefined

handleConn :: Application -> Socket -> IO ()
handleConn app socket = do
    req <- runResourceT $ sourceSocket socket $$ do
        requestLine <- parseRequestLine
        headers <- parseRequestHeaders
        return $ Request requestLine headers
    res <- liftIO $ app req
    liftIO $ sendResponse socket res
-- STOP
main = return ()
\end{lstlisting}
\subsection{Где же тело запроса?}
У нас всё было бы хорошо, если бы мы могли читать тело
запроса. Сейчас
приложению просто передается значение типа Request, которе завернуто в монаду IO. 
Оно не имеет доступа к входному потоку данных.

Это можно легко исправить, завернув значение типа Application в монаду Sink. 
Это тот же самый трюк, который мы исопльзовали в enumerator-based WAI 0.4. 
% FIXME? WAi? WTF?
Однако, есть две проблемы:

\begin{itemize}
\item Люди находят это неудобным. Они ожидают, что значение типа Request будет иметь в себе значение requestBody типа Source.
\item В некоторых случаях использование такого подхода становится невероятно тудным. Например, написать HTTP-прокси комбинируя WAI и http-энумераторы видится почти невозможным.
\end{itemize}
Существует ещё один недостаток инферсии управления. Наш код желает быть управляемым.
Он желает, чтобы ему дали что-то откуда можно брать данные, что-то куда их можно сложить и запуститься от них. Нам нужно какое-то решение этой проблемы.

Если вы считаете, что описанная проблема не так критична, то это потому, что вы не до конца погрузились в детали. На надо также учитывать
If you think that the situation I described with the proxy isn't so bad, it's because I've
gone easy on the details. We also need to take into account streaming the response body,
and the
streaming needs to happen on both the client and server side.
The simplest solution would be to just create a new Source and pass that to
the Application. Unfortunately, this will cause problems with our buffering. You
see, when we connect our source to the parseRequestLine and
parseRequestHeaders sinks, it made a call to recv. If the data
it received was not enough to cover all of the headers, it would issue another call. When
it had
enough data, it would stop. However, odds are that it didn't stop exactly at the end of
the headers. It likely consumed a bit of the request body as well.

If we just create a new source and pass that to the request, it will be missing the
beginning of the request body. We need some way to pass that buffered data along.

\subsection{Буферизованные источники (BufferedSource)}

Наконец, мы может представить последний тип данных в кондуитах: BufferedSource. Это абстрактный тип данных, но всё что он в действительности делает, это содержит мутабельную ссылку на буффер и соответствующий ему источник. Чтобы создать значение такого типа, вы можете воспользоваться функцией bufferSource.
\begin{verbatim}
bufferSource :: Resource m => Source m a -> ResourceT m (BufferedSource m a)
\end{verbatim}
Это небольшое улучшение позволит нам легко решить нашу дилемму связанную с веб-сервером.
Вместо соединения источника с синками, осуществляющими разбор, мы будем использовать 
BufferedSource. При закрытии каждого соединения, все оставшиеся данные будут складываться
обратно в буфер. Для нашего веб-сервера мы создадим BufferedSourceuse и будем использовать 
его для чтения запросов и их заголовков, а затем передадим этот BufferedSource в приложение, для чтения тел запросов.
% FIXME я ничего не понял.

\subsection{Классы типов}

Мы хотим уметь соединять буферизованый источник с синком, как будто это обычный источник.
Мы также хотим уметь комбинировать его с кондуитами. Чтоб сделать это удобным, в кондуитах
есть класс типов IsSource. Его экземляры предоставлены и для Source, и для BufferedSource. 
Операторы соединения (\verb#$$#) и комбинирования слева (\verb#$=#) используют этот класс типов.

There's one "gotcha" in the BufferedSource instance of this typeclass, so
let's explain it. Предположим, что мы хотим написать функцию копирования файлов, без буферизации. Это довольно стандартный случай использования кондуитов:
\begin{lstlisting}
sourceFile input $$ sinkFile output
\end{lstlisting}
Когда запускается этот код, то открывается и input-файл, и output-файл, данные копируются и затем оба файла закрываются. Изменим слегка код, чтобы использовать буферизацию:
\begin{lstlisting}
bsrc <- bufferSource $ sourceFile input
bsrc $$ isolate 50 =$ sinkFile output1
bsrc $$ sinkFile output2
\end{lstlisting}
А здесь, когда открывается и закрывается входной файл? Если соблюдать обычные правила для источников, то файл откроется в первой строчке, а закроется во второй. Следовательно, мы не сможем его использовать в третьей строчке!

Вместо этого, оператор \verb#$$# не закрывает файл. Следовательно, вы можете передавать буферизованый источник для выполнения тех действий, которые вам нужны, не заботясь, что дескриптор файла будет закрыт без вашего ведома.

Как мы упоминали ранее, инвариант гласит, что из источника невозможно забрать данные, если он вернул Closed. Чтобы облегчить работу с буферизоваными источниками, это инвариант десь не выполняется. It is the responsibility of
the BufferSource implementation to ensure that after the underlying
Source is closed, it is never used again.

Будьте осторожны: когда вы закончили работу с буферизованым источником, вам следует вручную вызывать bsourceClose. Однако, это обычно является оптимизацией, так как источник будет автоматически закрываться по окончанию работы функции runResourceT.

\subsection{Возвращаясь к веб-серверу}

Так как именно будет выглядеть наш веб-сервер?
\begin{lstlisting}
  -- START
import Data.ByteString (ByteString)
import Data.Conduit
import Data.Conduit.Network (sourceSocket)
import Control.Monad.IO.Class (liftIO)
import Network.Socket (Socket)

data RequestLine = RequestLine ByteString ByteString ByteString
type Headers = [(ByteString, ByteString)]
data Request = Request RequestLine Headers (BufferedSource IO ByteString)
data Response = Response
type Application = Request -> ResourceT IO Response

parseRequestHeaders :: Sink ByteString IO Headers
parseRequestHeaders = undefined

parseRequestLine :: Sink ByteString IO RequestLine
parseRequestLine = undefined

sendResponse :: Socket -> Response -> IO ()
sendResponse = undefined

handleConn :: Application -> Socket -> IO ()
handleConn app socket = runResourceT $ do
    bsrc <- bufferSource $ sourceSocket socket
    requestLine <- bsrc $$ parseRequestLine
    headers <- bsrc $$ parseRequestHeaders
    let req = Request requestLine headers bsrc
    res <- app req
    liftIO $ sendResponse socket res
-- STOP
main = return ()
\end{lstlisting}
Мы провели несколько небольших изменений. Во-первых, наше приложение теперь завернуто в 
\lstinline=ResourceT IO= монаду. Это не строгая необходимость, но это очень удобно:
приложение теперь может регистрировать функции освобождения ресурсов, которые выполнятся только тогда, когда ответ будет полностью послан клиенту.

Но главные изменения произошли в функции \lstinline=handleConn=. Сейчас мы начинаем работу
сразу с буферизации источника. Затем он используется дважды в нашей функции и передается в
приложение.

