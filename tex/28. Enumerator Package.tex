Пакет Enumerator

В связи с тем, что Yesod перешел на использование пакета conduits, эта глава больше не соответствует нынешнему Yesod. Она сохранена в качестве справки для тех, кто до сих пор используют enumerators.

Одной из развивающихся моделей в Haskell является enumerators. Они предназначены для решения проблем производства, изменения и потребления потоков данных. Enumerators считаются немного пугающиим, возможно, потому что:

* Существует несколько реализаций, все с несколько различными подходами.
* Некоторые из этих реализаций (на мой взгляд) используют сбивающюю с толку систему именования.
* Как правило, существующие руководства не нацелены непосредственно на конкретную реализацию, а больше работают на развитием интуиции, и не дают инструкции, как использовать библиотеку.

Инфраструктура Йесод использует enumerators за кулисами во многих местах: в WAI/Warp для работы с телами запроса и ответа, в Persistent для получения результатов запроса к базе данных, и в различных сторонних пакетах, таких как http-enumerator и xml-enumerator. Мы всегда используем пакет enumerators. Этот пакет является темой этой главы.

Эта глава состоит из трех основных разделов, каждый рассматривающий одно из основных понятий:

* Начнем с iteratees, которые являются потребителями (consumers). Им скармливаются данные и они что-то с ними делают.
* Далее мы рассмотрим enumerators, которые являются производителями (producers). Они скармливают данные iteratee.
* Наконец, мы рассмотрим enumeratees. Это трубы (pipes), которые получают данные от enumerators и в свою очередь передают их iteratee.

Вы можете быть удивлены, почему мы возиться с этим, когда большинство из этих проблем могут быть решены ленивым I/O. Основная проблема с ленивым I/O это его недетерминизм. Для получения дополнительной информации, то лучше пойти на оригинальный источник по этой теме: Олег.

Iteratees


Iteratee является потребителем данных. Это центральный тип пакета enumerator, на котором основаны другие типы.

Интуиция

Предположим, что мы хотим написать функцию, которая суммирует числа в списке. Пренебрёгши неинтересными деталями, такими как требованими к памяти, вполне хорошей реализацией будет:

\begin{lstlisting}
sum1 :: [Int] -> Int
sum1 [] = 0
sum1 (x:xs) = x + sum1 xs
\end{lstlisting}

Но давайте допустим, что у нас нету списка числа. Вместо этого, пользователь может вводить цифры в командной строке, и нажать "q", когда закончит. Другими словами, у нас есть такая функция:

\begin{lstlisting}
getNumber :: IO (Maybe Int)
getNumber = do
    x <- readLine
    if x == "q"
        then return Nothing
        else return $ Just $ read x
\end{lstlisting}

Мы можем написать новую функцию суммы так:

\begin{lstlisting}
sum2 :: IO Int
sum2 = do
    maybeNum <- getNumber
    case maybeNum of
        Nothing -> return 0
        Just num -> do
            rest <- sum2
            return $ num + rest
\end{lstlisting}

Это довольно неудобно быть винужденным писать две разные функции суммы только потому, что изменился наш источник данных. В идеале, мы бы хотели немного обобщить сущности. Начнем с того, что выделим сходство между этими двумя функциями: они обе возвращают значения только когда чисел больше нет. В случае sum1 мы проверяем пустой ли список, в sum2 мы проверяем на Nothing.

Тип данных Stream

Первый тип данных определеный в пакете enumerator:

\begin{lstlisting}
data Stream a = Chunks [a] | EOF
\end{lstlisting}

Конструктор EOF означает, что больше нет доступных данных. Конструктор Chunks просто позволяет нам поместить несколько частей данных вместе для повышения эффективности. Теперь мы можем переписать sum2 используя тип данных Stream:

\begin{lstlisting}
getNumber2 :: IO (Stream Int)
getNumber2 = do
    maybeNum <- getNumber -- using the original getNumber function
    case maybeNum of
        Nothing -> return EOF
        Just num -> return $ Chunks [num]

sum3 :: IO Int
sum3 = do
    stream <- getNumber2
    case stream of
        EOF -> return 0
        Chunks nums -> do
            let nums' = sum nums
            rest <- sum3
            return $ nums' + rest
\end{lstlisting}

Нельзя сказать, что это гораздо лучше, чем sum2, но по крайней мере это демонстрирует, как использовать тип данных Stream. Здесь проблема в том, что мы по-прежнему явно ссылаемся на функцию getNumber2, жёстко задавая источник данных.

Одним из возможных решений будет сделать источник данных аргументом функции суммы, а именно:

\begin{lstlisting}
sum4 :: IO (Stream Int) -> IO Int
sum4 getNum = do
    stream <- getNum
    case stream of
        EOF -> return 0
        Chunks nums -> do
            let nums' = sum nums
            rest <- sum4 getNum
            return $ nums' + rest
\end{lstlisting}

Это все хорошо, но давайте представим, что мы хотим иметь два источника данных для суммирования: значения которые пользователь вводит в командной строке, и,  допустим, числа которые мы читаем через соединение HTTP. Здесь перед нами проблема управления: sum4 управляет процессом вызывая getNum. Это модель вытягивания данных (pull data model). В enumerators используется модель инверсии управления, или же модель продавливания (inversion of control/push model), назначая ответсвенным за это enumerator. Это позволяет делать классные вещи, наподобии потребления данных из нескольких источников данных, а также становится проще писать enumerators, которые правильно работают с выделением (распределением) ресурсов.

Тип данных Step

Итак нам нужен новый тип данных, которые будут представлять состояние нашей операции суммирования. Мы позволим нашей операции находиться в одном из трех состояний:

* В ожидании новых данных.
* С уже подсчитаным результатом.
* Для удобства, мы также имеем состояние ошибки. Это не обязательно (например это состояние может быть смоделировано выбором монады вида EitherT), но это проще.

Как вы могли догадаться, эти состояния будут соответствовать трём конструкторам типа данных Step. Состояние ошибки моделируется с помощью Error SomeException, построеном на расширяемой системе исключений Haskell. Вот уже рассчитаный конструктор:

\begin{lstlisting}
Yield b (Stream a)
\end{lstlisting}

Здесь, a это вход для нашего iteratee, а b это выход. Этот конструктор позволяет одновременно произвести результат и сохранить "остатки" входных данных для другого iteratee, который может работать после нас. (Это не происходит в случае функции суммы, которая всегда потребляет все входные данные, но мы увидим и другие примеры, которые не потребляют все данные.)

Теперь вопрос заключается в том, как представлять состояние iteratee который ждёт дополнительных данных. Сперва вы можете захотеть объявить некоторый тип данных, чтобы представлять внутреннее состояние и передавать его каким-то образом. Но это не так, как оно сделано: вместо этого мы просто используем функцию (весьма в стиле Haskell, не так ли?):

\begin{lstlisting}
Continue (Stream a -> Iteratee a m b)
\end{lstlisting}

Эврика! Мы наконец увидели тип данных Iteratee! На самом деле, Iteratee очень скучный тип, который присутствует только для того, чтобы мы могли объявить крутые екземпляры (например, Monad) для наших функций. Iteratee определяется как:

\begin{lstlisting}
newtype Iteratee a m b = Iteratee (m (Step a m b))
\end{lstlisting}

А целиком тип данных Step выглядет так:

\begin{lstlisting}
data Step a m b = Error SomeException 
                | Yield b (Stream a) 
                | Continue (Stream a -> Iteratee a m b)
\end{lstlisting}

Это важно: Iteratee это просто newtype обертка вокруг Step завёрнутого в монаду. Просто имейте это в виду, когда будете смотреть на определения в пакете enumerator. Таким образом, зная это, мы можем представлять себе конструктор Continue как:

\begin{lstlisting}
Continue (Stream a -> m (Step a m b))
\end{lstlisting}

К этому определению гораздо легче подойти: эта функция берёт некоторое количество входных данных и возвращает новое состояние iteratee. Давайте посмотрим, как будет выглядеть наша функция суммы используя тип данных Step:

\begin{lstlisting}
sum5 :: Monad m => Step Int m Int -- вход Int, любая монада, выход Int
sum5 =
    Continue $ go 0 -- обычный подход, всегда начинайте с Continue
  where
    go :: Monad m => Int -> Stream Int -> Iteratee Int m Int
    -- Сложить новые входные данные с текущей суммой и создать новый Continue
    go runningSum (Chunks nums) = do
        let runningSum' = runningSum + sum nums
        -- Следующая строчка *страшная*, но хорошо, что есть 
        -- вспомогательные функции, что бы сделать это яснее.
        -- Больше про это ниже.
        Iteratee $ return $ Continue $ go runningSum'
    -- Произвести конечный результат
    go runningSum EOF = Iteratee $ return $ Yield runningSum EOF
\end{lstlisting}

Для того, чтобы запустить этот код, вы можете использовать run_ $ enumList 8 [1 .. 10] sum5. Но это использует чёрную магию Enumerator которую мы рассмотрим позже.

Первая действительная строка (Continue $ go 0) инициализирует наш iteratee в начальное состояние. Как и все другие функции суммирования мы должны явно заявить, что мы начинаем с 0. Настоящая рабочая лошадка это функция go. Обратите внимание, как мы передаём состояние iteratee в качестве первого аргумента go: это тоже очень распространенная подход в iteratees.

Мы должны обрабатывать два разных случая: когда получили EOF, функция go должна произвести значение Yield. (Она также может произвести Error, но определенно не может Continue.) В этом случае, мы просто возвращаем текущую сумму и говорим, что данных не осталось. Когда мы получаем некоторые входные данные с помощью Chunks, мы просто складываем их с текущей суммой и создаём новое Continue основаное на той же функции go.

Давайте теперь поработаем над тем, что бы сделать эту функцию немного красивее с помощью встроенных вспомогательных функций. Конструкция Iteratee . return достаточно обычна, чтобы оправдать вспомогательную функцию, а именно:

\begin{lstlisting}
returnI :: Monad m => Step a m b -> Iteratee a m b
returnI = Iteratee . return
\end{lstlisting}

Так к примеру,

\begin{lstlisting}
go runningSum EOF = Iteratee $ return $ Yield runningSum EOF
\end{lstlisting}

становится

\begin{lstlisting}
go runningSum EOF = returnI $ Yield runningSum EOF
\end{lstlisting}

Но даже это достаточно обычно, что бы захотеть вспомогательную функцию:

\begin{lstlisting}
yield :: Monad m => b -> Stream a -> Iteratee a m b
yield x chunk = returnI $ Yield x chunk
\end{lstlisting}

так наша строка становиться

\begin{lstlisting}
go runningSum EOF = yield runningSum EOF
\end{lstlisting}

Таким же образом,

\begin{lstlisting}
Iteratee $ return $ Continue $ go runningSum'
\end{lstlisting}

становиться

\begin{lstlisting}
continue $ go runningSum'
\end{lstlisting}

Monad instance for Iteratee

This is all very nice: we now have an iteratee that can be fed numbers from any monad and sum them. It can even take input from different sources and sum them together. (By the way, I haven't actually shown you how to feed those numbers in: that is in part 2 about enumerators.) But let's be honest: sum5 is an ugly function. Isn't there something easier?

In fact, there is. Remember how I said Iteratee really just existed to facilitate typeclass instances? This includes a monad instance. Feel free to look at the code to see how that instance is defined, but here we'll just look at how to use it:

\begin{lstlisting}
sum6 :: Monad m => Iteratee Int m Int
sum6 = do
    maybeNum <- head -- not head from Prelude!
    case maybeNum of
        Nothing -> return 0
        Just i -> do
            rest <- sum6
            return $ i + rest
\end{lstlisting}

That head function is not from Prelude, it's from the Data.Enumerator module. Its type signature is:

\begin{lstlisting}
head :: Monad m => Iteratee a m (Maybe a)
\end{lstlisting}

which basically means give me the next piece of input if it's there. We'll look at this function in more depth in a bit.

Go compare the code for sum6 with sum2: they are amazingly similar. You can often build up more complicated iteratees by using some simple iteratees and the Monad instance of Iteratee.

Interleaved I/O

Alright, let's look at a totally different problem. We want to be fed some strings and print them to the screen one line at a time. One approach would be to use lazy I/O:

\begin{lstlisting}
lazyIO :: IO ()
lazyIO = do
    s <- lines `fmap` getContents
    mapM_ putStrLn s
\end{lstlisting}

But this has two drawbacks:

* It's tied down to a single input source, stdin. This could be worked around with an argument giving a datasource.
* But let's say the data source is some scarce resource (think: file handles on a very busy web server). We have no guarantees with lazy I/O of when those file handles will be released.

Let's look at how to write this in our new high-level monadic iteratee approach:

\begin{lstlisting}
interleaved :: MonadIO m => Iteratee String m ()
interleaved = do
    maybeLine <- head
    case maybeLine of
        Nothing -> return ()
        Just line -> do
            liftIO $ putStrLn line
            interleaved
\end{lstlisting}

The liftIO function comes from the transformers package, and simply promotes an action in the IO monad to any arbitrary MonadIO action. Notice how we don't really track any state with this iteratee: we don't care about its result, only its side effects.

Implementing head

As a last example, let's actually implement the head function.

\begin{lstlisting}
head' :: Monad m => Iteratee a m (Maybe a)
head' =
    continue go
  where
    go (Chunks []) = continue go
    go (Chunks (x:xs)) = yield (Just x) (Chunks xs)
    go EOF = yield Nothing EOF
\end{lstlisting}

Like our sum6 function, this also wraps an inner "go" function with a continue. However, we now have three clauses for our go function. The first handles the case of Chunks []. To quote the enumerator docs:

(Chunks []) is used to indicate that a stream is still active, but currently has no available data. Iteratees should ignore empty chunks.

The second clause handles the case where we are given some data. In this case, we yield the first element in the list, and return the rest as leftover data. The third clause handles the end of input by returning Nothing.

Exercises

1. Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.
2. Write the opposite enumeratee, eg Enumeratee Word8 Char m b.
3. Create a quickcheck property that ensures that these two functions work correctly.

Summary

* Enumeratees are the pipes connecting enumerators to iteratees.
* The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.
* You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.
* Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.
* You can always compose multiple enumeratees together, such as in http-enumerator.

Enumerators

Whereas an Iteratee is a consumer, an Enumerator is a producer. At a high level, an Enumerator is in fact an Iteratee transformer, moving an Iteratee from one state to a new one. Let's look at how this works.

Extracting a value

So far, we've written a few iteratees, but we still don't know how to extract values from them. To start, let's remember that Iteratee is just a newtype wrapper around Step:

\begin{lstlisting}
newtype Iteratee a m b = Iteratee { runIteratee :: m (Step a m b) }
\end{lstlisting}

First we need to unwrap the Iteratee and deal with the Step value inside. Remember also that Step has three constructors: Continue, Yield and Error. We'll handle the Error constructor by returning our result in an Either. Yield already provides the data we're looking for.

The tricky case is Continue: here, we have an iteratee that is still expecting more data. This is where the EOF constructor comes in handy: it's our little way to tell the iteratee to finish what it's doing and get on with things. If you remember from above, I said a well-behaving iteratee will never return a Continue after receiving an EOF; now we'll see why:

\begin{lstlisting}
extract :: Monad m => Iteratee a m b -> m (Either SomeException b)
extract (Iteratee mstep) = do
    step <- mstep
    case step of
        Continue k -> do
            let Iteratee mstep' = k EOF
            step' <- mstep'
            case step' of
                Continue _ -> error "Misbehaving iteratee"
                Yield b _ -> return $ Right b
                Error e -> return $ Left e
        Yield b _ -> return $ Right b
        Error e -> return $ Left e
\end{lstlisting}

Fortunately, you don't need to redefine this yourself: enumerator includes both a run and run_ function. Let's go ahead and use it on our sum6 function:

\begin{lstlisting}
main = run_ sum6 >>= print
\end{lstlisting}

If you run this, the result will be 0. This emphasizes an important point: an iteratee is not just how to process incoming data, it is the state of the processing. In this case, we haven't done anything to change the initial state of sum6, so we still have the initial value of 0.

To give an analogy: think of an iteratee as a machine. When you feed it data, you modify the internal state but you can't see any of those changes on the outside. When you are done feeding the data, you press a button and it spits out the result. If you don't feed in any data, your result is the initial state.

Adding data

Let's say that we actually want to sum some numbers. For example, the numbers 1 to 10. We need some way to feed that into our sum6 iteratee. In order to approach this, we'll once again need to unwrap our Iteratee and deal with the Step value directly.

In our case, we know with certainty that the Step constructor we used is Continue, so it's safe to write our function as:

\begin{lstlisting}
sum7 :: Monad m => Iteratee Int m Int
sum7 = Iteratee $ do
    Continue k <- runIteratee sum6
    runIteratee $ k $ Chunks [1..10]
\end{lstlisting}

But in general, we won't know what constructor will be lying in wait for us. We need to properly deal with Continue, Yield and Error. We've seen what to do with Continue: feed it the data. With Yield and Error, the right action in general is to do nothing, since we've already arrived at our final result (either a successful Yield or an Error). So the "proper" way to write the above function is:

\begin{lstlisting}
sum8 :: Monad m => Iteratee Int m Int
sum8 = Iteratee $ do
    step <- runIteratee sum6
    case step of
        Continue k -> runIteratee $ k $ Chunks [1..10]
        _ -> return step
\end{lstlisting}

Enumerator type synonym

What we've done with sum7 and sum8 is perform a transformation on the Iteratee. But we've done this in a very limited way: we've hard-coded in the original Iteratee function (sum6). We could just make this an argument to the function:

\begin{lstlisting}
sum9 :: Monad m => Iteratee Int m Int -> Iteratee Int m Int
sum9 orig = Iteratee $ do
    step <- runIteratee orig
    case step of
        Continue k -> runIteratee $ k $ Chunks [1..10]
        _ -> return step
\end{lstlisting}

But since we always just want to unwrap the Iteratee value anyway, it turns out that it's more natural to make the argument of type Step, ie:

\begin{lstlisting}
sum10 :: Monad m => Step Int m Int -> Iteratee Int m Int
sum10 (Continue k) = k $ Chunks [1..10]
sum10 step = returnI step
\end{lstlisting}

This type signature (take a Step, return an Iteratee) turns out to be very common:

\begin{lstlisting}
type Enumerator a m b = Step a m b -> Iteratee a m b
\end{lstlisting}

Meaning sum10's type signature could also be expressed as:

\begin{lstlisting}
sum10 :: Monad m => Enumerator Int m Int
\end{lstlisting}

Of course, we need some helper function to connect an Enumerator and an Iteratee:

\begin{lstlisting}
applyEnum :: Monad m => Enumerator a m b -> Iteratee a m b -> Iteratee a m b
applyEnum enum iter = Iteratee $ do
    step <- runIteratee iter
    runIteratee $ enum step
\end{lstlisting}

Let me repeat the intuition here: the Enumerator is transforming the Iteratee from its initial state to a new state by feeding it more data. In order to use this function, we could write:

\begin{lstlisting}
run_ (applyEnum sum10 sum6) >>= print
\end{lstlisting}

This results in 55, exactly as we'd expect. But now we can see one of the benefits of enumerators: we can use multiple data sources. Let's say we have another enumerator:

\begin{lstlisting}
sum11 :: Monad m => Enumerator Int m Int
sum11 (Continue k) = k $ Chunks [11..20]
sum11 step = returnI step
\end{lstlisting}

Then we could simply apply both enumerators:

\begin{lstlisting}
run_ (applyEnum sum11 $ applyEnum sum10 sum6) >>= print
\end{lstlisting}

And we would get the result 210. (Yes, (1 + 20) * 10 = 210.) But don't worry, you don't need to write this applyEnum function yourself: enumerator provides a $$ operator which does the same thing. Its type signature is a bit scarier, since it's a generalization of applyEnum, but it works the same, and even makes code more readable:

\begin{lstlisting}
run_ (sum11 $$ sum10 $$ sum6) >>= print
\end{lstlisting}

$$ is a synonym for ==<<, which is simply flip >>==. I find $$ the most readable, but YMMV (YMMV).

Some built-in enumerators

Of course, writing a whole function just to pass some numbers to our sum function seems a bit tedious. We could easily make the list an argument to the function:

\begin{lstlisting}
sum12 :: Monad m => [Int] -> Enumerator Int m Int
sum12 nums (Continue k) = k $ Chunks nums
sum12 _ step = returnI step
\end{lstlisting}

But now there's not even anything Int-specific in our function. We could easily generalize this to:

\begin{lstlisting}
genericSum12 :: Monad m => [a] -> Enumerator a m b
genericSum12 nums (Continue k) = k $ Chunks nums
genericSum12 _ step = returnI step
\end{lstlisting}

And in fact, enumerator comes built in with the enumList function which does this. enumList also takes an Integer argument to indicate the maximum number of elements to stick in a chunk. For example, we could write:

\begin{lstlisting}
run_ (enumList 5 [1..30] $$ sum6) >>= print
\end{lstlisting}

(That produces 465 if you're counting.) The first argument to enumList should never affect the result, though it may have some performance impact.

Data.Enumerator includes two other enumerators: enumEOF simply passes an EOF to the iteratee. concatEnums is slightly more interesting; it combines multiple enumerators together. For example:

\begin{lstlisting}
run_ (concatEnums
        [ enumList 1 [1..10]
        , enumList 1 [11..20]
        , enumList 1 [21..30]
        ] $$ sum6) >>= print
\end{lstlisting}

This also produces 465.

Some non-pure input

Enumerators are much more interesting when they aren't simply dealing with pure values. In the first part of this tutorial, we gave the example of the user entering numbers on the command line:

\begin{lstlisting}
getNumber :: IO (Maybe Int)
getNumber = do
    x <- getLine
    if x == "q"
        then return Nothing
        else return $ Just $ read x

sum2 :: IO Int
sum2 = do
    maybeNum <- getNumber
    case maybeNum of
        Nothing -> return 0
        Just num -> do
            rest <- sum2
            return $ num + rest
\end{lstlisting}

We referred to this as the pull-model: sum2 pulled each value from getNumber. Let's see if we can rewrite getNumber to be a pusher instead of a pullee.

\begin{lstlisting}
getNumberEnum :: MonadIO m => Enumerator Int m b
getNumberEnum (Continue k) = do
    x <- liftIO getLine
    if x == "q"
        then continue k
        else k (Chunks [read x]) >>== getNumberEnum
getNumberEnum step = returnI step
\end{lstlisting}

First, notice that we check which constructor was passed, and only perform any actions if it was Continue. If it was Continue, we get the line of input from the user. If the line is "q" (our indication to stop feeding in values), we do nothing. You might have thought that we should pass an EOF. But if we did that, we'd be preventing other data from being sent to this iteratee. Instead, we simply return the original Step value.

If the line was not "q", we convert it to an Int via read, create a Stream value with the Chunks datatype, and pass it to k. (If we wanted to do things properly, we'd check if x is really an Int and use the Error constructor; I leave that as an exercise to the reader.) At this point, let's look at type signatures:

\begin{lstlisting}
k (Chunks [read x]) :: Iteratee Int m b
\end{lstlisting}

If we simply left off the rest of the line, our program would typecheck. However, it would only ever read one value from the command line; the >>== getNumberEnum causes our enumerator to loop.

One last thing to note about our function: notice the b in our type signature.

\begin{lstlisting}
getNumberEnum :: MonadIO m => Enumerator Int m b
\end{lstlisting}

This is saying that our Enumerator can feed Ints to any Iteratee accepting Ints, and it doesn't matter what the final output type will be. This is in general the way enumerators work. This allows us to create drastically different iteratees that work with the same enumerators:

\begin{lstlisting}
intsToStrings :: (Show a, Monad m) => Iteratee a m String
intsToStrings = (unlines . map show) `fmap` consume
\end{lstlisting}

And then both of these lines work:

\begin{lstlisting}
run_ (getNumberEnum $$ sum6) >>= print
run_ (getNumberEnum $$ intsToStrings) >>= print
\end{lstlisting}

Exercises

1. Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.
2. Write the opposite enumeratee, eg Enumeratee Word8 Char m b.
3. Create a quickcheck property that ensures that these two functions work correctly.

Summary

* Enumeratees are the pipes connecting enumerators to iteratees.
* The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.
* You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.
* Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.
* You can always compose multiple enumeratees together, such as in http-enumerator.

Enumeratees

The final piece in the puzzle is the Enumeratee. This is a combination of an Iteratee (it consumes some data) and an Enumerator (is passes it other data on). It can be thought of as a stream transformer, or as a pipe connecting an Enumerator to an Iteratee.

Generalizing getNumberEnum

Earlier, we created a getNumberEnum function with a type signature:

\begin{lstlisting}
getNumberEnum :: MonadIO m => Enumerator Int m b
\end{lstlisting}

If you don't remember, this means getNumberEnum produces a stream of Ints. In particular, our getNumberEnum function read lines from stdin, converted them to ints and fed them into an iteratee. It stopped reading lines when it saw a "q".

But this functionality seems like it could be useful outside the realm of Ints. We may like to deal with the original Strings, for example, or Bools, or a bunch of other things. We could easily define a more generalized function which simply doesn't do the String to Int conversion:

\begin{lstlisting}
lineEnum :: MonadIO m => Enumerator String m b
lineEnum (Continue k) = do
    x <- liftIO getLine
    if x == "q"
        then continue k
        else k (Chunks [x]) >>== lineEnum
lineEnum step = returnI step
\end{lstlisting}

Cool, let's plug this into our sumIter function (I've renamed the sum6 function from the previous two parts):

lineEnum $$ sumIter

Actually, that doesn't type check: lineEnum produces Strings, and sumIter takes Ints. We need to modify one of them somehow.

\begin{lstlisting}
sumIterString :: Monad m => Iteratee String m Int
sumIterString = Iteratee $ do
    innerStep <- runIteratee sumIter
    return $ go innerStep
  where
    go :: Monad m => Step Int m Int -> Step String m Int
    go (Yield res _) = Yield res EOF
    go (Error err) = Error err
    go (Continue k) = Continue $ \strings -> Iteratee $ do
        let ints = fmap read strings :: Stream Int
        step <- runIteratee $ k ints
        return $ go step
\end{lstlisting}

What we've done here is wrap around the original iteratee. As usual, we first need to unwrap the Iteratee constructor and the monad to get at the heart of the Step value. Once we have that innerStep value, we pass it to the go function, which simply transforms that values in the Stream value from Strings to Ints.

Even more general

Of course, it would be nice if we could apply this transformation to *any* iteratee. To start with, let's just pass the inner iteratee and the mapping function as parameters.

\begin{lstlisting}
mapIter :: Monad m => (aOut -> aIn) -> Iteratee aIn m b -> Iteratee aOut m b
mapIter f innerIter = Iteratee $ do
    innerStep <- runIteratee innerIter
    return $ go innerStep
  where
    go (Yield res _) = Yield res EOF
    go (Error err) = Error err
    go (Continue k) = Continue $ \strings -> Iteratee $ do
        let ints = fmap f strings
        step <- runIteratee $ k ints
        return $ go step
\end{lstlisting}

We could call this like:

\begin{lstlisting}
run_ (lineEnum $$ mapIter read sumIter) >>= print
\end{lstlisting}

Nothing much to see here, it's basically identical to the previous version. What's funny is that enumerator comes built in with a map function to do just this, but it has a significantly different type signature:

\begin{lstlisting}
map :: Monad m => (ao -> ai) -> Enumeratee ao ai m b
\end{lstlisting}

since:

\begin{lstlisting}
type Enumeratee aOut aIn m b = Step aIn m b -> Iteratee aOut m (Step aIn m b)
\end{lstlisting}

that's equivalent to:

\begin{lstlisting}
map :: Monad m => (aOut -> aIn) -> Step aIn m b -> Iteratee aOut m (Step aIn m b)
\end{lstlisting}

What's with all this extra complication in type signature? Well, it's not necessary for map itself, but it is necessary for a whole bunch of other similar functions. But let's focus on this map for a second so we don't get lost: the first argument is the same old mapping function we had before. The second argument is a Step value. This isn't really so surprising: in our mapIter, we took an Iteratee with the same parameters, and we internally just unwrapped it to a Step.

But what's happening with that return value? Remembering the meanings for all these datatypes, it's an Iteratee which will be fed a stream of aOuts and return a Step (aka, a new iteratee, right?). This kind of makes intuitive sense: we've introduced a middle man which accepts input from one source and transforms a Step to a newer state.

But now perhaps the trickiest part of the whole thing: how do we actually use this map function? It turns out that an Enumeratee is close enough in type signature to an Enumerator that we can just do:

\begin{lstlisting}
map read $$ sumIter
\end{lstlisting}

But the type signature on that turns out to be a little bit weird:

\begin{lstlisting}
Iteratee String m (Step Int m Int)
\end{lstlisting}

Remembering that an Iteratee is just a wrapped up Step, what we've got here is an iteratee that takes Strings and returns an Iteratee, which in turn takes Ints and produces an Int. Having this fancy result allows us to do one of our great tricks with iteratees: plug in data from multiple sources. For example, we could plug some Strings into this whole ugly thing, run it, get a new iteratee which takes Ints, feed that some Ints and get an Int result.

(If all that went over your head, don't worry. I won't be talking about that kind of stuff any more.)

But often times, we don't need all of that power. We just want to stick our enumeratee onto our iteratee and get a new iteratee. In our case, we want to attach our map onto the sumIter to produce a new iteratee that takes Strings and returns Ints. In order to do that, we need a function like this:

\begin{lstlisting}
unnest :: Monad m => Iteratee String m (Step Int m Int) -> Iteratee String m Int
unnest outer = do -- using the Monad instance of Iteratee
    inner <- outer -- inner :: Step Int m Int
    go inner
  where
    go (Error e) = throwError e
    go (Yield x _) = yield x EOF
    go (Continue k) = k EOF >>== go
\end{lstlisting}

We can then run our unholy mess with:

\begin{lstlisting}
run_ (lineEnum $$ unnest $ map read $$ sumIter) >>= print
\end{lstlisting}

And actually, the unnest function is available in Data.Enumerator, and it's called joinI. So we should really write:

\begin{lstlisting}
run_ (lineEnum $$ joinI $ map read $$ sumIter) >>= print
\end{lstlisting}

Skipping

Let's write a slightly more interesting enumeratee: this one skips every other input value.

\begin{lstlisting}
skip :: Monad m => Enumeratee a a m b
skip (Continue k) = do
    x <- head
    _ <- head -- the one we're skipping
    case x of
        Nothing -> return $ Continue k
        Just y -> do
            newStep <- lift $ runIteratee $ k $ Chunks [y]
            skip newStep
skip step = return step
\end{lstlisting}

What's interesting about the approach here is how similar it looks to an Enumerator. We're doing a lot of the same things: checking if the Step value is a Continue; if it's not, then simply return it. Then we capitalize on the Iteratee Monad instance, using the head function to pop two values out of the stream. If there's no more data, we return the original Continue value: just like with an Enumerator, we don't give an EOF so that we can feed more data into the iteratee later. If there is data, we pass it off to the iteratee, get our new step value and then loop.

And what's cool about enumeratees is we can chain these all together:

\begin{lstlisting}
run_ (lineEnum $$ joinI $ skip $$ joinI $ map read $$ sumIter) >>= print
\end{lstlisting}

Here, we read lines, skip every other input, convert the Strings to Ints and sum them.

Real life examples: http-enumerator package

I started working on these tutorials as I was working on the http-enumerator package. I think the usage of enumeratees there is a great explanation of the benefits they can offer in real life. There are three different ways the response body can be broken up:

* Chunked encoding. In this case, the web server gives a hex string specifying the length of the next chunk and then that chunk. At the end, it sends a 0 to indicate the end of that response.
* Content length. Here, the web server sends a header before any of the body is sent specifying the total length of the body.
* Nothing at all. In this case, the response body lasts until an end-of-file.

In addition, the body may or may not be GZIP compressed. We end up with the following enumeratees, each with type signature Enumeratee ByteString ByteString m b: chunkedEncoding, contentLength and ungzip. We then get to do something akin to:

\begin{lstlisting}
let parseBody x =
        if ("transfer-encoding", "chunked") `elem` responseHeaders
            then joinI $ chunkedEncoding $$ x
            else case mlen of
                    Just len -> joinI $ contentLength len $$ x
                    Nothing -> x -- no enumeratee applied at all
let decompress x =
        if ("content-encoding", "gzip") `elem` responseHeaders
            then joinI $ ungzip $$ x
            else x
run_ $ socketEnumerator $$ parseBody $ decompress $ bodyIteratee
\end{lstlisting}

We create a chain: the data from the server is fed into the parseBody function. In the case of chunked encoding, the data is processed appropriately and then headers are filtered out. If we are dealing with content length, then only the specified number of bytes are read. And in the case of neither of those, parseBody is a no-op.

Whatever the case may be, the raw response body is then fed into decompress. If the body is GZIPed, then ungzip inflates it, otherwise decompress is a no-op. Finally, the parsed and inflated data is fed into the user-supplied bodyIteratee function. The user remains blissfully unaware of any steps the data took to get to him/her.

Exercises

1. Write an enumeratee which takes hex chars (eg, "DEADBEEF") to Word8s. Its type signature should be Enumeratee Char Word8 m b.
2. Write the opposite enumeratee, eg Enumeratee Word8 Char m b.
3. Create a quickcheck property that ensures that these two functions work correctly.

Summary

* Enumeratees are the pipes connecting enumerators to iteratees.
* The strange type signature of an Enumeratee hides a lot of possible power. Especially notice how similar their type signatures are to Enumerators.
* You can merge an Enumeratee into an Iteratee with joinI $ enumeratee $$ iteratee.
* Don't forget that you can use the Monad instance of Iteratee when creating your own enumeratees.
* You can always compose multiple enumeratees together, such as in http-enumerator.
